"Beat Witch" by Robert Patten

The story headline is "An interactive loneliness".
Use American dialect.
Use serial comma.
Use no scoring.
The story genre is "Horror".
The release number is 1.
The story creation year is 2023.


The story description is "Countless people drop dead, and only you know why. Armed with only a notepad and a Walkman, you venture into the nearly empty city to face the source."


Include Simple Chat by Mark Tilford.
Include Punctuation Removal by Emily Short.
Include Room Description Control by Emily Short.
Include Numbered Disambiguation Choices by Aaron Reed.
Include Epistemology by Eric Eve.
Include Basic Screen Effects by Emily Short.
Include Room Description Headings by Erwin Genstein.



Include Vorple Multimedia by Juhana Leinonen.
Include Vorple Status Line by Juhana Leinonen.






[     if Vorple is supported:
]


Release along with the "Vorple" interpreter.


Release along with cover art, interpreter, and a website.


[Log:
Saturday:
1:45 to  2 - 15
6:50 to 7:07 - 17
7:25 to 7:39 - 14
7:45 to 7:57 - 12
9:03 to 9:12 - 9
9:17 to 9:19 - 2
9:25 to 9:29 - 4
9:40 to 10:05 -25
--total 40
11 to 11:05 - 5
11:07 to 11:25 -18
11:27 to 11:44 - 17
11:47 to 11:51 - 4
11:57 to 12:06 - 9
12:08 to 12:18 - 10



Monday
8:23 to 8:50 - 27
8:55 to 9:04 - 9
9:40 to 10:02 - 22
10:08 to 10:17 - 9
10:20 to 10:27 - 7

Tuesday
5:50 to 6:03 - 13
6:07 to 6:30 - 23
8:55 to 9:17 - 22
9:20 to 10:50 - 90


Thursday
8:09 to 8:32 - 23

Friday
1:16 to 1:45 - 29
11 to 1:10 - 130

Saturday
8:28
fde
]

Volume Testing (not for release)

Test me with "take off helmet/g/jump/wear helmet/take life from steve/leave"


Volume Misc

Book Sayings

[A tip from Zarf on intfiction.org -- this avoids Inform 7's unwanted line breaks.]

To say period: (- print "."; -). 
To say exclamation: (- print "!"; -).
To say question: (- print "?"; -).

Part Any Key - Not for release

[The skein in Inform 7 is getting confused over the wait for any key parts, so I'm skipping over them as I code, but the reader will have them.]

To press any key:
	do nothing;

Part Any Key 2 - For release only

To press any key:
	wait for any key;

Part Any Key 3

To say keypress:
	press any key;

Book Filter Commands

After reading a command:
	resolve punctuated titles;

Book Death

To die:
	now the player is dead;
	end the story saying "You die";	
	

Instead of attacking someone:
	say "Outright physical attacks aren't your style.";


Book Epistemology

When play begins:
	now every subject is unfamiliar.
	
unmasked-subject is a subject.

Book Examining

A thing can be examined or unexamined. A thing is usually unexamined.

Before examining something:
	now the noun is examined.
	



Volume Status Line

Book Status Line Text

To say polly-state:
	if a musical scene is happening and music-harm? is true:
		say "Dying";
	otherwise if the player is wounded:
		say "Wounded";
	otherwise if the player is strong:
		say "Strong"; 
	otherwise if the player is stronger:
		say "Stronger";
	otherwise if the player is godly:
		say "Divine";
	otherwise if the player is dead:
		say "Dead";
	otherwise if the player is uncomfortable:
		say "Tortured";
	otherwise if the player is drained:
		say "Weak";
	otherwise:
		say "Healthy";

	
playing-music? is a truth state that varies.
	
Every turn:
	now playing-music? is true;

To say play-noplay:
	if the player is dead:
		say "Deep silence";
	otherwise if a musical scene is happening or a semi-musical scene is happening or a person in the location is iphony or iphone-humming? is true:
		[say "Playing: ";]
		say "[unicode 9834] "; [musical note]
	otherwise:
		say "No music";
		now playing-music? is true;


Definition: A person is iphony if it encloses the iPhone.

To say musical-state:
	if the player is dead or playing-music? is false:
		do nothing;
	otherwise if Torture is happening:
		say "Music from ceiling (soft)";
	otherwise if Musical Walkman is happening:
		say "Walkman Side A";
	otherwise if Margo's Music is happening:
		say "Walkman Side A (faint)";
[		say "side [if the tape-side of the tape is A-side]A[otherwise]B[end if]/earphones[if the player wears the earphones]on[otherwise]off";
]	otherwise if the iPhone is in the location or a person in the location is iphony or iphone-humming? is true:
		say "iPhone ([if the player is wearing the cowl or iphone-humming? is true]faint[otherwise]loud[end if])";

Book Standard Status Line

Rule for constructing the status line:
	fill status bar with Table of Fancy Status;


Table of Fancy Status
left	central	right
"[play-noplay][musical-state]"	"[location]"	"[polly-state]"


Book Vorple Status Line

[Vorple requires us to construct its own status line.]

The left hand Vorple status line is "[play-noplay][musical-state]".
The middle Vorple status line is "[location]".
The right hand Vorple status line is "[polly-state]".


When play begins:
	if Vorple is supported:
		construct the Vorple status line with 3 columns;

Book Vorple Status Line (Mobile)

The mobile Vorple status line is "[play-noplay][musical-state] | [location] | [polly-state]";

Volume Locations

Book Going

[We don't much in the way of maps in most of this game, but we do have a lovely instead rules that are almost as good!]

Understand "get out of here" as exiting.


Instead of going or exiting:
	if the location is Staging Area:
		if Escape is happening:
			try silently attacking the magnet-door;
		otherwise:
			say "[unless Deadly iPhone is happening]You can't go anywhere with Dr. Steve in your way. And the door's magnetically sealed[else][one of]A steady rhythm claws deep into muscle and bone. The world is a torrent of burning sound.[paragraph break][or][stopping]You can't go anywhere when you're fully exposed to music[end unless].";
	otherwise:
		continue the action;
		
Instead of going south when the location is South Side:
	if Lobby is unvisited:
		say "You limp inside.";
		now the player is in Lobby;
	otherwise:
		continue the action;

Book Kinds

forbidden is a kind of backdrop. [A simple backdrop can still be touched, which isn't ideal when the backdrop (scenery in more than one room) is something like a sun.]

Instead of touching a forbidden:
	say "You can't touch [the noun]." 


Book Staging Area

Staging Area is a room. "Translucent plastic veils enclose what was once a basketball court. Hoops and bleachers are faint shadows. High above you, the scrubbers sterilize any intruding air.[paragraph break]Dozens of tables bear medical equipment. In front of them is a whiteboard.[one of][steve-directions][or][stopping]" [steve-directions defined in Unmasked scene, even though it's not actually part of it.]

Part Staging Area - Stuff

The whiteboard is scenery in Staging Area. The description of the whiteboard is "It reads:[paragraph break][fixed letter spacing]Timeline:[line break]March 2, 12:22 p.m. - Power goes out - all devices, entire city - EMP device?[line break]March 2, approximately 1:00 p.m. - First signs of plague appear - business district[line break]March 3 - Plague firmly in residential areas[line break]March 4 - Quarantine in place[paragraph break]Terrorists? Bacterium, virus, fungus, prion?[line break]Spread: Airborne?[line break]Known symptom: Sudden death, no external or internal signs of injury[variable letter spacing]";

Understand "white/board/timeline" or "time line" as the whiteboard.

Understand "plastic/veils/veil/sheets/sheet/curtain/curtains" as the sheets.

The hoops are a forbidden in Staging Area. The hoops are plural-named. The description of the hoops is "The basketball hoops are circles of darkness at either end of the court."

Understand "nets" or "nets" or "basketball/hoop" or "basketball/hoops" or "shadow/circles" or "shadows" or "circles of darkness" or "darkness" as the hoops. The hoops are plural-named. 

The bleachers are a forbidden in Staging Area. The bleachers are plural-named. The description of the bleachers is "The bleachers are empty and silent."

Instead of entering the bleachers:
	say "You prefer to stand.";

Understand "benches" or "bench" as the bleachers.

The scrubbers are a forbidden in Staging Area. The scrubbers are plural-named. The description of the scrubbers is "Gray boxes are suspended on cables high above you. They are to filter germs, and are thus useless in this case."

Understand "box/boxes/scrubber/scrubbers/cables" as the scrubbers.

The tables are a supporter in Staging Area. The tables are scenery. The tables are plural-named. "The tables are the long, fold-out kind every high school has crammed in a closet. They bend under the weight of medical supplies."

Understand "table" as the tables.

The equipment is scenery on the tables. The description of the equipment is "Laptops, syringes, centrifuges, gauze, antibiotics -- everything an emergency response team might need to combat a typical epidemic."

Understand "medical/equipment/computers/computer/laptops/laptop/syringes/syringe/centrifuge/centrifuges/gauze/bandage/bandages/antibiotics/medicine/supplies" as the equipment.

Instead of taking the equipment:
	say "Useless. The hazmat crew might as well have brought doughnuts to feed the dead, for all the good this equipment will do.";
	
The sheets are scenery in Staging Area. The sheets are plural-named. "The plastic curtains around and above you -- extra protection against the boogey-germs that supposedly lurk outside -- give this place a netherworld feel."

Understand "translucent/veil/sheet" as the sheets.

the magnet-door is scenery in Staging Area. The printed name of the magnet-door is "the door". 

The description of the magnet-door is "A black, magnetically sealed door that's supposed to keep the plague outside. It does not.[if the player is strong or the player is stronger][paragraph break]You can easily break it now.[end if]"
 
The magnet-door can be shattered.

Understand "magnet/magnetic/sealed/door/doorway" as the magnet-door.

Instead of opening or pushing or pulling the magnet-door:
	unless the player is strong or the player is stronger:
		say "Here's the problem: Dr. Steve's in front of the door, it's magnetically sealed, you have no idea where the controls are, and it would take more strength than you have to open it. [paragraph break]More strength than you have now, that is.";
	otherwise:
		try silently attacking the noun;
	


Book City

City is a region. Street is a room in City. 

The piles are a backdrop. The piles are plural-named. The piles are in City.

The description of the piles is "[one of]Looking at the fires fills you with repulsive fascination. [or][stopping]The flames have almost finished consuming the blackened, twisted shapes that used to be people."

Instead of touching the piles: say "Not in a million years."

Instead of taking the piles: say "You wish you could place all the bodies in marked graves, but you have neither the time nor the strength."

Understand "remains/fire/fires/flames/embers/mound/mounds/corpses/piles/shapes" as the piles.


The buildings are a backdrop. The buildings are in City. The buildings are plural-named.

Understand "apartment" or "apartments" or "apartment/building" or "apartment/buildings" as the buildings when the player is in Street.


The description of the buildings is "[if the location is Street]Lonely apartments where residents sleep forever[otherwise]High office buildings line the street. The tallest has its lights on, mysteriously[end if]."

Understand "office/building" or "office/buildings" as the buildings.

[Before entering the buildings when the player is in South Side and Lobby is unvisited:
	try silently going south instead;]

The asphalt is a backdrop. The asphalt is in City. The description of the asphalt is "Asphalt and concrete reflect the night."

Understand "street/road/night/maze/city/concrete/cement/sidewalk/sidewalks" as the asphalt.

The streetlights are a backdrop. The streetlights are in City. The streetlights are plural-named. The description of the streetlights is "Dark."

Understand "street lights" or "streetlamps" or "streetlamp" or "street lamp" or "street lamps" or "lamp" or "lamps" or "street light" as the streetlights.

The moon is a backdrop. The moon is in City. The description of the moon is "The moonlight is a sickly silver that ebbs and glows."

Understand "silver/moon/moonlight" or "moon light" as the moon. Understand "light" as the moon when the location is Street.

Part Street

Street is a room. [* Start here. Need to implement apartments. And South Side needs more scenery. Implement blood, skyscraper/doors, asphalt, other buildings, dark streetlamps, moonlight, a corpse. Some are backdrops in Lobby as well. Oh yeah, implement the Lobby!]

The description of Street is "[street-description]"

The printed name of Street is "Streets".

To say street-description: 
	say "[one of]CRACK[period][or]The cries of the responders and frantic rhythm of the dance music are gone. Your breathing is heavy and fast. You have to walk.[paragraph break]You are among the shadows of apartment buildings, all dark, all teeming with death.[paragraph break]You pass a pile of burning bodies, reddish embers in the dark. Sadly, there's nothing unusual about the fires -- but one of the corpses appears to have rolled off the pile before it ignited. You can't blame Jessica's team for missing one among endless thousands.[or]Dying flames feast on human remains. An unburned corpse appears to have rolled off one of the mounds[period][stopping]"

the corpse is a person in Street. The description of the corpse is "Slightly bloated, one of the corpses is a young man in his 20s wearing rollerblades.[one of][paragraph break]You wish you could GIVE LIFE to him, but you've tried this kind of thing before, and nothing.[or][stopping]"

When play begins:
	now the corpse is dead.

Understand "young/man" or "dead/body" as the corpse.

The rollerblades are a wearable thing. The rollerblades are plural-named. The corpse wears the rollerblades. The description of the rollerblades is "The rollerblades are well worn. This person probably spent a lot of time on them before his sudden demise."

Understand "roller/blades" or "roller/skates" as the rollerblades.
	
Before going when the location is Street:
	say "You follow the uneasiness before you.[paragraph break]You hear [if the corpse is examined]the [italic type]click clack[roman type] of rollerblades[otherwise]footsteps[end if] behind you. When you turn, you see only splotches of embers in the night.";
	
[* implement darkness and gleams]

Instead of going when the location is Street:
	now the player is in South Side.


Part Business District

Business District is a region. Business District is in City.

Chapter South Side


[The description of Street is "[one of]The cries of the responders aren't far behind -- nor is the faint beat of Dr. Steve's dance music.[paragraph break]The pain in your leg is too great. You stumble and black out for a few seconds.[paragraph break]When you awake, you find your fall had knocked the helmet off. You manage to stand and don't bother picking it up.[paragraph break][or][stopping][if Street is unvisited]You take in your surroundings in the moonlight. [end if]You are near the entrance of what appears to be a high office building. It may be the tallest skyscraper in town. The doors are open[if the Lobby is unvisited], and you're sure the beat witch is inside[end if].[if the helmet is in Street][one of][paragraph break]Your helmet is here.[or][stopping][end if]";]

South Side is a room in Business District. The printed name of the South Side is "South Side of Skyscraper". [South Side is north from the Lobby.

Report going south when the location is South Side:
	say "You limp inside";]

[* Change room description of South Side and skyscraper when we are ready for when Polly falls and when she is triumphant.]

The description of South Side is "[one of]The pain in your leg explodes. You stumble and black out for a few seconds.[paragraph break]When you awake, you find your fall knocked the helmet off. You manage to stand, but you leave the helmet where it is.[paragraph break][or][stopping][if South Side is unvisited]You take in your surroundings in the moonlight. [end if]You're a little more lucid now, and you notice streets downtown are laid out and labeled in a grid, and the entrance of this office building is to the north. It's the tallest skyscraper around[if the Lobby is unvisited] and also happens to be where she is lurking[end if].[if South Side is visited] The doors are open, and the lights are on[otherwise][paragraph break]Lights on some of the floors flicker to life, and the front doors appear to open on their own.[paragraph break]She is waiting[end if].[if the helmet is in South Side][paragraph break]Your helmet is here.[end if]";

the skyscraper is scenery in South Side. The description of the skyscraper is "The skyscraper is a gray shadow. Some of its windows glow."

The double doors are north of South Side and south of Lobby. The double doors are a door. The double doors are plural-named. The double doors are open.

The description of the double doors is "The double doors are made of a sturdy, thick glass." 

Understand "glass/double/doors" or "glass/double/door" or "front/door" or "front/doors" or "entrance" as the double doors.


Before opening the double doors:
	if the noun is open:
		say "The doors are already open." instead;
	otherwise if the noun is closed and the player is not godly:
		say "[one of]A bottomless sense of hopelessness swallows you. Why not get fate over with? [or][stopping]You try to open the doors, but they may as well be part of the wall." instead;

Before closing the double doors:
	if the noun is open:
		say "[one of]You strain against the doors, but they don't move[or]You can't move the doors[or][stopping]." instead;
	otherwise:
		say "The doors are already closed." instead;

Understand "tallest building" or "tall building" or "taller building" or "tall/tallest/highest/high/big/biggest/skyscraper/gray/grey/shadow/building" as the skyscraper. 

The windows are a thing. The windows are part of the skyscraper. The windows are plural-named. The description of the windows is "Some of the tall skyscraper's windows glow a pale yellow."

Does the player mean entering the skyscraper:
	it is very likely.

Before entering the skyscraper:
	try going north instead;
	

Before going a direction (called way) when the location is South Side:
	if way is north:
		continue the action;
	otherwise if way is up:
		if the player is not godly:
			say "If you want to go up, you're going to have to go inside the building." instead;
		[* We'll be inserting some code here for Polly to climb up the building later. ]
	otherwise:
		say "You must face the beat witch. The doors are to the north." instead;


Understand "pale/yellow/light/lights/window" as the windows.

Chapter East Side

East Side is a room in Business District.

Chapter North Side

North Side is a room in Business District.

Chapter West Side

West Side is a room in Business District.

Book Oficina

[Inside the skyscraper]

Oficina is a region. Hall is a region. Hall is in Oficina. [In Hall, we have the ever-present stairs. We have a container called office later, so I'm resorting to Spanish since my imagination is full.]

The floor is a backdrop. It is in Oficina. The description of the floor is "Carpet in some places and polished stone in others, it's just the floor."

Understand "stone" or "carpet" or "ground" as the floor.

Part Lobby

[I can't decide on a name of the company, so I'm going to be able to change it in one place.]

To say company:
	say "Tau Labs"; 
	
Lobby is a room.  Lobby is in Oficina. The description of Lobby is "The only clue the white-marbled lobby does not belong to a 5-star hotel is the gigantic logo behind the desk[one of][or], behind which sits [ted-or-corpse][stopping].[paragraph break]On one side of the desk is a doorless opening that is marked as TO STAIRWELL. On the other side is an elevator.[one of][or][paragraph break]Double doors lead to the street. The mob outside [if Bloodthirsty Mob has happened for more than nine turns]would gladly tear you apart[otherwise]is trying to break through them[end if].[stopping]"

The printed name of Lobby is "Main Lobby".

The logo is scenery. The description of the logo is "A flat silver disc bears the blocky emblem of [company] in muted reds and blues." The logo is in Lobby.  Understand "silver/sign/flat/disc/disk/emblem" as the logo.

The marble is scenery in Lobby. "The blue-veined marble is polished and probably expensive."

The desk is a supporter in Lobby. The desk is fixed in place. Understand "table" as the desk.

Instead of looking under the desk:
	say "You have a feeling Ted wouldn't like you snooping under the desk to find the door opener. Besides, why would you let your would-be killers inside?";

The opening is scenery in Lobby. Understand "stairs" or "stairwell" or "stair well" or "twilight/dimness" or "passage" as the opening. The description of the opening is "A passage leads into a twilight dimness, presumably to the stairs."

Instead of taking the opening:
	try silently entering the noun;

Instead of entering the opening:
	now Ted is in Base;
	say "You've lost all sense of direction again. You stagger toward the stairs. You trip, and Ted immediately catches you. As jarring as it is for your to touch another person, you have no choice but to lean on his stiff shoulder[if ted-zombie is familiar] and try to forget you're relying on a corpse[end if].";
	now the player is in Base; [As in Base of Stairs. Inform gets confused, so we work around this.]
	now ted-identity is familiar;
	
Instead of going a direction (called way) when the location is Lobby:
	if way is up or way is down:
		say "(using the elevator)";
		try entering the elevator;
	otherwise:
		say "(entering the stairwell)";
		try entering the opening;

An elevator-lift is a kind of thing. An elevator-lift is always fixed in place.

There is an elevator-lift called the elevator. the elevator is in Lobby. 

Understand "button" or "lift" or "elevator/lift button" or "writing" or "elevator/lift doors" as the elevator. Understand "face" or "frowny face" or "frown" or "writing" as the elevator when the player is in Lobby. 

Understand "use [elevator-lift]" as pushing.

Instead of pushing or taking an elevator-lift:
	try entering the noun;


Instead of entering or opening an elevator-lift:
	if the location is Lobby:
		say "You push the elevator button. Nothing happens[if the elevator is unexamined]. The writing on the elevator doors may have something to do with this[end if].";
	otherwise:
		say "The elevator doors were mangled shut in the fire."

The description of the elevator is "Someone wrote 'OUT OF ORDER. USE STAIRS.' on the elevator. In purple crayon. With a heart over the 'I' in 'STAIRS.'"

After deciding the scope of the player when the location is the Lobby:
	place the South Side in scope;

Part Stairwell

There is a room called Base. The printed name of Base is "Bottom of Stairs". Base is in Hall.

The description of Base is "You are at the base of what appears to be endlessly zigzagging stairs.[paragraph break][if Denise is comatose]A girl lies motionless on the floor. You believe she's breathing.[one of] Ted points at you, then her.[or][stopping][paragraph break]You don't care how he knows that you aren't a force of destruction only, that you can make life flow two ways ... but you are so weak! You want to help, but you doubt you have much life to GIVE her -- it would likely mean your death.[paragraph break]On the other hand, taking what meager life must remain in the child is a possibility. It would give you the strength you need to at least limp up the stairs to face the beat witch. The girl would not be strong enough to survive you draining her, though[otherwise][end if].[if thinking-powers-broken is familiar][one of][paragraph break](None of this matters if your powers don't work.)[or][stopping][end if]";

The stairs are a backdrop. The stairs are in Hall. The stairs are plural-named. Understand "stairwell" or "stair well" or "stair" or "upper/depths" as the stairs.

The description of the stairs is "In contrast to the marble of the lobby, the stairwell is pure utility. Iron stairs lead into the skyscraper's upper depths."

Before climbing the stairs:
	try going up instead; [* We may have to make this more nuanced.]

the bullet is a thing. The description of the bullet is "The bullet glimmers with your blood."

Understand "blood/bloody/metal/bullet/shell/cartridge" as the bullet.

the crayon is a thing. The description of the crayon is "The crayon is a purple stub." 

When play begins:
	now Denise has the crayon.

Understand "purple/crayon/stub" as the crayon.

the voice is scenery. "It's a girl's voice, and you can't tell where it's coming from."

Understand "girls/voice" as the voice.

Instead of doing something to the voice:
	say "[description of the voice]";


Part 32nd Floor

Hall is a region. 

the wires are a backdrop. The wires are in Hall. Understand "sparks/spark/sparking/sparkling/wire/wires/ceiling"as the wires. The description of the wires is "Sparking wires hiss from the ceiling."

the scorch marks are a backdrop. The scorch marks are in Hall. Understand "scorchmarks" or "scortchmarks" or "scortch marks" or "burn marks" or "burn mark" or "scorch mark" or "scortch marks" or "wall" or "walls" or "flame" or "flames" or "fire" or "ceiling" as the scorch marks. The scorch marks are plural-named. The description of the scorch marks is "A wave of flame appears to have swept across this part of the office building, half-destroying the walls and ceiling."

the hallway is a backdrop. The hallway is in Hall. Understand "hall" or "corridor" or "passage" or "hall way" as the hallway. The description of the hallway is "The red lights aren't bright enough for you to make out [unless the location is Upper Lobby or the location is Breakroom]either end[otherwise]the other end[end unless] of the hallway."

the lights are a backdrop. The lights are in Hall. Understand "red/emergency/light/lights" as the lights. The lights are plural-named. The description of the lights is "The emergency lights brighten and dim rhythmically."

[*start here and implement writing for different parts of Hall. also need to implement emergency lights for Hall.]

Chapter Upper Lobby

Upper Lobby is a room in Hall. 

[The description of Upper Lobby is "[one of]Your awaken in lights that bathe everything in an undulating scarlet.[paragraph break]You leap to your feet -- it's as if you were never shot.[paragraph break]Your relief explodes into panic. You pat your pockets in a frenzy -- your Walkman and notepad are gone.[paragraph break][or][stopping][one of]You find yourself[or]You are[stopping] in a lobby is marked as '32 - Research and Development.' [one of][or]Everything is lit in pulsing red emergency lights.[stopping][paragraph break]Scorch marks twist over the walls and furniture, and wires dangle from the ceiling. [pipe-presence][paragraph break]The floor bears a charred decorative compass that tells you [upper-lobby-directions]. The elevator looks busted.[paragraph break]
 on one of the more intact walls are the words 'BENNY ABANDONED YOU' in crayon accompanied by a happy face.[one of][paragraph break]Your fists clench, and you fight a boiling nausea. That ... creature listened to side B of your tape.[or][stopping][paragraph break][one of]'Who's Benny?' someone behind you asks.[paragraph break]A man in a soiled shirt and ragged tie is supporting himself against the wall. He points to his what's left of his badge. 'Are you ... alive?' He shakes his head. 'Nope, nope, can't be. You're the only one, Hal. The living gone. Just Hal. And the witch.'[paragraph break]He tries to squint you out of existence[or]Hal pretends you don't exist[stopping].[paragraph break][one of]Something crackles above you, and organ music gasps from hidden speakers[or]Hidden speakers above you spew organ music.[stopping] It's so soft, it won't kill you, but it's loud enough to make your head burn."]


The description of Upper Lobby is "[if Upper Lobby is unvisited]You awaken to fingers grasping your wrist.[paragraph break]You leap to your feet. Your leg feels fine -- it's as if you were never shot.[paragraph break]In the undulating red light, you face a man in a soiled shirt and ragged tie. His smile is wide but strained.[paragraph break]'A pulse! I didn't think I'd meet another living person,' he says. He suffocates you in a hug. You can't remember the last time anyone showed you affection, and you're not sure what to do.[paragraph break]Why do you feel lighter?[paragraph break]You shove him away and pat your pockets in a frenzy. Your Walkman and notepad are gone.[paragraph break]The man doesn't seem to notice your sudden panic.[paragraph break]'I'm Hal, chief executive of this establishment,' he says while showing you his corporate badge, which is unreadable, chewed-off plastic dangling from a lanyard. 'Usually the witch gives me little notes when I ... wake up, but I think this one's for you.'[paragraph break]Hal points to the only wall that hasn't been gnawed by flame.[paragraph break]Scribbled in crayon are the words 'BENNY ABANDONED YOU' accompanied by a happy face.[paragraph break]You are enveloped in a boiling nausea. How dare she listen to side B of your tape![paragraph break]You look around you as you try to relax -- or at least stop shaking.[paragraph break][end if]You are in a lobby labeled as '32 - Research and Development.' Everything is lit in pulsing red emergency lights. Scorch marks twist over the walls and furniture, and wires dangle from the ceiling.[paragraph break]The floor bears a charred decorative compass that tells you the stairway door behind you is to the west, and the hallway beyond the lobby goes east. The elevator looks busted.[pipe-presence][one of][or][hal-room-desc][stopping]"; [* Start here. Add elevator, stairs and hall. Also need to work on Torture music scene.]

the pipe is a thing. The description of the pipe is "A steel pipe about the length of a yardstick that must have fallen from the ineffective fire suppression system." 

the pipe is in Upper Lobby.

Understand "steel/pipe/tubeyardstick" as the pipe.

To say pipe-presence:
	say "[if the pipe is in the location][paragraph break]A steel pipe is on the floor[period][end if]";

the lift is an elevator-lift. The description of the lift is "The elevator's doors have twisted permanently shut."

the lift is in Upper Lobby. Understand "elevator" or "button" or "lift" or "elevator/lift button" or "elevator/lift doors" as the lift. the printed name of the lift is "the elevator".

the writing is scenery in Upper Lobby. Understand "words/writing" or "happy/face" or "benny/abandoned you" or "message" as the writing. The description of the writing is "Written in crayon[if the elevator is examined], just like the message in the main floor lobby[end if], the words 'BENNY ABANDONED YOU' leave you feeling filthy, ashamed and violated over the fact that the other beat witch listened to something personal of yours."

the compass is scenery in Upper Lobby. "Once a golden decoration painted onto the marble floor, the half-burned compass now tells you [upper-lobby-directions]."

Understand "marble/decoration/decorative/compass/floor" as the compass.

To say upper-lobby-directions:
	say "the stairway door behind you is to the west, and the hallway beyond the lobby goes east".

the furniture is scenery in Upper Lobby. "Couches and desks are disfigured skeletons of ash." Understand "couch/couches/desk/desks/sofa/sofas/skeleton/skeletons/ash/ashen/wood" as the furniture.

Instead of entering the furniture:
	say "[one of]The weakened, blackened wood collapses under your weight[if Hal is in the location].[paragraph break]'Careful there,' Hal says[end if][or]The furniture is too damaged for that[stopping]."

the stairs-door is an openable container. the stairs-door is in Upper Lobby. the stairs-door is closed. the stairs-door is fixed in place. The printed name of the stairs-door is "the door to the stairs." 

Understand "door " or "door to the stairs" or "door to stairs" or "stair door" or "stairs" or "stairwell" or "stair well" or "ash" as the stairs-door. The stairs-door is privately-named.

The description of the stairs-door is "This is a large metal door covered in ash."

Instead of opening the stairs-door:
	try going west.
	
Instead of going west when the location is Upper Lobby and Explore is happening:
	say "[one of]You try to open the door to the stairwell. It is jammed[or]The door won't budge[stopping].";

Chapter Stairwell

Upper Stairs is a room. Upper Stairs is west from Upper Lobby.

[* We'll add a description later of Polly being marched up to the roof.]

Chapter Corridor

Corridor is a room in Hall. "The northern wall is mostly crumbled and charred to reveal the remains of a lab. The southern wall didn't fare much better, though an office door appears to be intact. [if the pipe is in the location][pipe-presence] [end if]A memo has been posted near the door.[hal-room-desc]"

Corridor is east from Upper Lobby. 

the lab is a backdrop. The lab is in Corridor and End of Corridor. The description of the lab is "Among the smashed and burned remains of computers, tables, and schematics is a rectangular, metallic thing the size of a bathtub[if the memo is examined] -- this must be the battery[end if]."

Instead of entering the lab:
	say "The lab is a ruined mess of wires and equipment that leaves you no room for you.";

Understand "tables/table/computer/computers/schematic/schematics/remains/smashed/wires/equipment" as the lab.

the battery is a forbidden. the battery is in Corridor. 

Understand "rectangle/metal/metallic/bathtub/thing" or "bath tub" as the battery. 

The battery is privately-named. The printed name of the battery is "metal thing".

Understand "semper/battery/prototype" as the battery when the memo is examined.

After examining the memo:
	now the battery is not privately-named;

The description of the battery is "It appears the fiery destruction emanated from [if the memo is examined]the battery[otherwise]the metal thing[end if]."

The memo is scenery. The memo is in Corridor. Understand "office memo" or "paper" as the memo.

The description of the memo is "Written on a clean piece of paper someone must have posted here after the fire are the words:

[italic type]To all valued engineers:

I am pleased to announce that last week's small-scale tests of the Semper battery were a success. Although we are years away from using it to power submarines and military bases, I am confident these preliminary test results represent a step forward for this company.

A few of you have expressed concerns the full-sized prototype would set off a reaction that would result in, and I quote, 'a localized fiery explosion as well as a massive electromagnetic pulse that would have catastrophic effects on electronic devices in a 20-mile radius.' 

My brother Ted was opposed to further testing for this reason and unfortunately did not hide that fact from lower-level employees. However, we had an extensive conversation yesterday, and he is now as confident as I am that full testing will be a success and will be harmless to us and the surrounding area. 

To the naysayers among us, I say if a respected engineer like Ted can change his opinion, please consider re-evaluating yours.

The first full-scale test will take place at 11 a.m. Please join us in the 32nd floor breakroom at noon for a celebratory luncheon.

Regards,
[line break]Hal Tau
[line break]Chief Executive Officer
[line break][company][roman type][one of][paragraph break]'I didn't leave that there,' Hal mutters. He avoids eye contact. 'I wrote it, sure, but I would never have put my ... my lowest moment on display.'[or][stopping]";

Instead of taking the memo:
	say "[one of]You think about taking the memo with you, but what good would it do?[or]An office memo will not help you defeat the beat witch.[stopping]";

[Our door and office are fake.]

the office is an openable container. the office is in Corridor. the office is fixed in place. Understand "office/door" as the office.

the office is closed.

Instead of examining the office: 
	say "[if the office is open][office-description][otherwise]A thick door recently cracked with heat stands between you and what's inside this office[end if].";

Instead of going south when the location is Corridor:
	try opening the office;
	
Before going when the location is Corridor and Hal Drama is happening:
	unless the current action is going south:
		if the office is open:
			say "Hal closes the office before you leave.";
			now the office is closed;
		

Before entering the office:
	try going south instead;

Before opening the office:
	if the office is closed:
		unless the office has been open:
			say "You open the office door.[paragraph break][office-description].[paragraph break]Hal dry heaves behind you.[paragraph break]'There are offices stuffed like this all over the building,' he says between gasps. 'But that doesn't make it any easier.'";
			now the office is open instead;
		otherwise:
			say "[one of]You try to open the office door again, but Hal blocks you.[paragraph break]'I never want to see that again,' he says[or]Hal won't let you[stopping]." instead;
	otherwise:
		say "You don't dare go inside the office.[paragraph break][office-description]." instead;
	

[To say hal-whimpers:
	say "[one of]Hal whimpers in a corner[or]'Close it,' Hal whispers.[or]Hal is shaking[or]Hal [at random]";]

Report closing the office:
	say "You close the office door. Hal's breathing becomes more regular.[paragraph break]'Let's not do that again,' he says.[paragraph break]" instead;
		
To say office-description:
	say "What was once an office for middle management is now stuffed with corpses. On the bottom of the pile are those with blistered flesh and singed lab coats -- not from any purposeful cremation, but from how they died. The unburned are on the top. There must be dozens of bodies here";

the corpses are people. the corpses are plural-named. The description of the corpses is "[office-description].". Understand "lab coats" and "blackened/flesh" and "unburned" as the corpses.

the corpses are in the office.

When play begins:
	now the corpses are dead;
	

Before going east when the location is Corridor:
	say "[if hal-flashback-spoken is false]'Wait,' Hal says. He cringes a little[period][paragraph break][hal-drama-action][otherwise][run paragraph on][end if]"; [This allows us to make sure hal-flashback is printed, where he mentions Polly must have become a beat witch when she hit puberty. Important exposition we would otherwise miss if the player moves through the hallway too fast.]

Chapter End of Corridor

There is a room called End of Corridor. "[if Explore is happening or Little Fight is happening][one of]This is where it ends.[paragraph break]A tall figure emerges from red-tinged shadow. According to the company-issued nametag, the young woman is Margo Harris. She's wearing earplugs.[paragraph break]'You found me! [italic type]Speechless[roman type], Polly?' She erupts into hollow giggles. 'You're so confused and underpowered -- oooh, and angry too! Does little Polly Dolly get mad when someone listens to her tape?'[paragraph break]Hal is wailing. 'How can it be you, Margo? I weighed in on the hiring process of everyone -- even interns like you. You didn't seem like a beat witch at all! I mean, you -- you can talk!'[paragraph break]'Toys must not squeak,' Margo says with a click of the tongue.[paragraph break]She nods to an arched opening at the east wall.[or]The hallway ends at an arched opening, beyond which is the breakroom. And sandwiches, apparently.[paragraph break]Margo's arms are folded in impatience.[stopping][otherwise]This is the end of the corridor.[end if][hal-room-desc]" 

End of Corridor is east from Corridor. End of Corridor is in Hall.

The gap is an enterable container in End of Corridor. The gap is fixed in place.

The description of the gap is "A wide arched gap in the far wall leads to the breakroom."

Understand "break room" or "breakroom" or "gap" or "opening" or "arch"  as the gap.

Instead of entering the gap:
	try going east.

Chapter Breakroom

Breakroom is east from End of Corridor.

To say ted-breakroom:
	say "[if ted is dead]Ted lies below the counter[otherwise if Hal is on the chairs]Ted keeps Hal in his seat[otherwise]Ted stares through you unblinkingly[end if], and";

To say hal-breakroom:
	if Hal is not in Breakroom or Hal is on the chairs:
		do nothing;
	otherwise:
		say " [if Hal is drained]Hal is limp on the floor[otherwise if Hal is dead]Hal lies dead here[otherwise if Hal is strong]Hal's slight glow casts the room in ethereal shadows[end if][unless Hal is on the chairs][period][end if]";

To say margo-breakroom:
	if Margo is not in Breakroom:
		do nothing;
	otherwise:
		say " [if False Climax First is happening]Margo has you by the arms and won't let you get up from the chair you're sitting in[otherwise if Margo is dangling]Margo is dangling from a ledge outside the window[otherwise if Margo is struggling]Margo struggles against Hal[']s grip and is close to the edge of the broken window[otherwise]Margo waits for your next move[end if][period]";

To say characters-breakroom:
	say "[one of][or][paragraph break][ted-breakroom][hal-breakroom][margo-breakroom][stopping]";



The description of Breakroom is "[one of]The flames left the breakroom untouched.[paragraph break][or][stopping]This place was once ready to party: balloons are wilted rainbows on the ceiling, and a counter next to a refrigerator [if Ted is ted]is strewn with boxes of sandwiches[otherwise]bears boxes filled with flies and sandwiches[end if].[paragraph break]Small, round tables and pale chairs are angled toward a [if the window is broken]shattered[otherwise]wide[end if] window that overlooks a gray twilight fog[if the TV is in the Breakroom].  Next to the window is a small but bulky TV set[one of] that is controlled by a black remote on a table[or][stopping][end if].[characters-breakroom]".



[The description of Breakroom is "[one of]The flames left the breakroom untouched.[paragraph break][or][stopping][if the current action is looking][generic-breakroom-desc][otherwise][one of][generic-breakroom-desc][or][stopping][characters-breakroom][end if]".

To say generic-breakroom-desc:
	say "This place was once ready to party: balloons are wilted rainbows on the ceiling, and a counter next to a refrigerator [if Ted is ted]is strewn with boxes and sandwiches[otherwise]bears boxes filled with flies and sandwiches[end if].[paragraph break]Small, round tables and pale chairs are angled toward a [if the window is broken]shattered[otherwise]wide[end if] window that overlooks a gray twilight fog[if the TV is in the Breakroom][period]  Next to the window is a small but bulky TV set[one of] that is controlled by a black remote on a table[or][stopping][end if].";]

the TV is scenery in Breakroom. The description of the TV is "This bulky old TV set probably came from a manager's office years ago. You haven't seen one like it since pogs were a thing."

Instead of switching on or switching off the TV:
	say "The television doesn't have any visible buttons. It must only be operated by remote.";

the remote is a thing. Margo holds the remote. The description of the remote is "The remote is a large black rectangle with more buttons than a sane person should deal with."

Understand "button" or "buttons" or "remote/control" as the remote.

Instead of pushing the remote:
	try taking the noun;

To say gray:
	say "Gray, corporate, and plastic";
 
some chairs are an enterable supporter in Breakroom. They are plural-named and fixed in place. The description of the chairs are "[gray]."

The can't travel in what's not a vehicle rule does nothing when the player is on the chairs. 

Report exiting when the player is on the chairs and the player is drained:
	say "You stumble out of the chair to the floor." instead;

Report entering the chairs when the player is drained:
	say "You clamber back onto the chair." instead;

[the printed name of some chairs is "a chair".]

[The chairs can be binding.] [That is, we can't get up from them.]

Before taking something when chair-binding? is true:
	unless the noun is enclosed by the player:
		say "You can't reach [the noun]." instead;

Rule for printing an enclosure description when the described enclosure is the chairs:
	if Breakroom is unvisited:
		say "";
	otherwise: 
		say " (on a chair)";

Report entering the chairs:
	say "You get on a chair." instead;
	
Before exiting when the player is on the chairs:
	if chair-binding? is true:
		rule fails instead;
	otherwise:
		say "You get off the chair[if the player has the shard], and the shard rolls off your lap and onto the floor among the other pieces of glass[end if].";
		if the player has the shard:
			remove the shard from play;
		now the player is in Breakroom instead;

Instead of going when chair-binding? is true:
	rule fails instead;

the mesas are a supporter in Breakroom. the mesas are plural-named. the mesas are fixed in place. The description of the mesas is "[gray]." Understand "table/tables" as the mesas.

the mesas are privately-named. the printed name of the mesas is "the tables".

the balloons are scenery in Breakroom. The description of the balloons is "The balloons are colorful, deflated dreams."

the balloons are plural-named.

Before doing something to the balloons:
	unless the current action is examining:
		say "You can't reach the useless balloons." instead;

Understand "useless/balloon/baloon/balloons/balloon/rainbow/rainbows" as the balloons.


Understand "corporate/plastic/chair/chairs/seat/seats" as the chairs.

the refrigerator is scenery in Breakroom. The description of the refrigerator is "You don't even want to know what's inside." Understand "fridge" or "frige" or "refrigerater" or "refridgerator" or "refridgerater" as the refrigerator.

Instead of opening the refrigerator:
	say "Dare you?[paragraph break]You don't.";

the counter is a supporter in Breakroom. The counter is fixed in place. The description of the counter is "A black counter bears what once was lunch." Understand "counters" as the counter.

the sandwiches are scenery in Breakroom. The description of the sandwiches is "Covered in flies and an oozing mold, these sandwiches were once ... ham and turkey?[if Ted is dead] They are scattered all over the floor.[end if]"; 

The sandwiches are plural-named. Understand "fly/flies/maggot/maggots/ooze/oozing/oozy/mold/moldy/sandwich/sandwiches/bread/lunch/box/boxes/ooze/luncheon/food/ham/turkey/meat/grub" or "boxes of sandwiches" or "sand wich" or "sand wiches" as the sandwiches.

Before eating the sandwiches:
	say "You can't make yourself get close to the 'food.'" instead;

Instead of taking or touching the sandwiches:
	say "You gag at the thought of touching the sandwiches.";

the window is scenery in Breakroom. 

The window can be broken.

The description of the window is "[unless the window is broken]A wide window that once offered a view of thousands of lights below now overlooks a dense twilight mist[otherwise]A gaping hole and a shattered mess of glass are all that's left of the window[end unless]."

Understand "glass/shards/hole/ledge/shard/mess" or "piece of glass" or "piece" or "glass piece" as the window when the window is broken.

The shard is a thing. The description of the shard is "The shard is a long, jagged piece of glass from the broken window. It looks sharp enough to cut something."

Rule for clarifying the parser's choice of the shard:
	do nothing;

Instead of taking the window when the window is broken:
	say "[one of]You reach to take a piece of glass. A sharp edge bites into your finger. You banish the pain by sucking the single drop of blood[if Margo is in Breakroom and Margo is not dangling]. [paragraph break]'Attacking me with glass would do you no good anyway,' Margo says[end if][or]You're not cutting yourself again[stopping].";

Understand "glass/shard" or "piece of glass" as the shard when the player has the shard.

Does the player mean doing something to the shard:
	it is very likely.

Instead of dropping the shard:
	say "You feel like you're going to need this. Like, right now.";


the ledge is scenery. 

The description of the ledge is "[if Margo is dangling]Margo dangles from a[otherwise]A[end if] thin band of concrete."

Understand "concrete/cement/ledge/band" as the ledge.


Every turn when the window is broken and the ledge is not in Breakroom:
		now the ledge is in Breakroom;

[Understand "glass/shards" as the window when the window is broken.]

the fog is a forbidden. the fog is in Breakroom and Roof and South Side. The description of the fog is "A drifting silver fog hides the [if the location is South Side]top of the skyscraper[otherwise]darkness of the city below[end if]."

Understand "twilight/night/mist/nothingness/abyss/darkness/dark" as the fog.

Part Roof

Roof is a room.

Volume People

A witch is a kind of person. A witch is always female.

Definition: A person is nonwitch if it is not a witch.

Book Player

The player is a witch called Polly. Understand "polly/miller" as Polly.

The description of the player is "[if the player wears a helmet][features-hidden][otherwise][polly-description][end if]."

To say features-hidden:
	say "Your features are hidden under the hazmat helmet[if the player is strong or the player is stronger], but you are emitting a small glow[end if]";

the glow is scenery. Understand "light" or "slight/glow" or "aura" as the glow.

Instead of taking the glow:
	say "It's already yours.";

The description of the glow is "Your body has a small aura of white light."

Every turn:
	if the player is strong or the player is stronger:
		now glow is in the location;
	otherwise:
		now the glow is off-stage;
	


To say polly-description:
	if the player is alive:
		say "Despite your tangled hair and mud-streaked skin, your flawless face looks as if it belongs to a fairy tale princess ... or a handmaid of death";
	otherwise if the player is strong: 
		say "You are glowing slightly";
	otherwise if the player is stronger:
		say "You have more life than you need and are glowing slightly";
	otherwise if the player is godly:
		say "You glow like an angel";
	otherwise if the player is wounded:
		say "You a cold shade of pale, and your leg is bleeding";
	otherwise if the player is uncomfortable:
		say "The soft music is making you tremble. You breathe in shallow gasps";
	otherwise: [should only fire if the player is drained, which should never happen]
		say "You are a wreck";


A face is a kind of thing. A face is part of every person. 

[Polly's face is called polly-face.

Understand "your face" or "my face" or "features" or "your features" or "my features" as polly-face.]

Before examining a face (called the mug):
	if the mug is part of a person (called the mug-holder) and the description of the mug is "":
		say "[description of mug-holder][line break]" instead;
	otherwise:
		continue the action;


leg is a thing. leg is part of the player.  The description of the leg is "[unless the player is wounded]Your leg's feeling fine[otherwise]Your leg has been shot and is bleeding terribly[end if]."

hair is part of the player. The description of the hair is "[if the player wears a helmet][features-hidden][otherwise]Knotted, greasy, and wild[end if]."

Understand "my hair" or "your hair" as the hair.

skin is part of the player. The description of the skin is "[if the player wears a helmet][features-hidden][otherwise]Your skin is covered in grime[end if]."

Understand "my skin" or "your skin" as the skin.

Understand "bullet/wound" as the leg when Street is visited.

Understand "legs" as the leg.

Chapter Polly's Alignment

[Although the optimal ending is for Polly to be good, we want to give the player the ability to make her embrace the beat witch everyone thinks she is.]

Alignment is a kind of value. The player has alignment. the alignments are good, gray, and evil. [We already used "neutral" for Denise.]

When play begins:
	now the player is good.


Part Clothes

Chapter Hazmat Suit

A hazmat-suit is a kind of thing. A hazmat-suit is wearable. 

The player wears a hazmat-suit called the uniform. The printed name of the uniform is "your hazmat suit".

your pockets are a thing. your pockets are plural-named. your pockets are part of the uniform. 

Understand "pocket" or "my pocket" or "your pocket" as your pockets. Understand "lap" or "my lap" or "your lap" as your pockets when chair-binding? is true.

Understand "look in [something]" as examining.

Instead of examining the pockets:
	try taking inventory.

The description of hazmat-suits is "Bulky, yellow, and an inefficient means of keeping out this plague."

Understand "protective/yellow/hazmat/suit/pants" as the uniform.

To say hazmat-helmet-desc:
	say "The helmet is a massive cowl that protects the head, face, and neck from whatever microbes emergency crews [if Bloodthirsty Mob has happened]once believed had[otherwise]believe have[end if] rained upon this city";

A helmet is a kind of thing. A helmet is wearable.

When play begins:
	repeat with H running through helmets:
		now the description of H is "[hazmat-helmet-desc].[run paragraph on]"

The player wears a helmet called cowl. The printed name of the cowl is "your helmet".

After examining the cowl: 
	say "[if the cowl is worn by the player][line break]It muffles sounds other than your own breathing, and its tinted visor makes everything slightly blurry. Wearing it makes you feel like a far-removed observer.[otherwise if the player is wounded and the player carries the cowl][line break]It is cumbersome and heavy.[end if]";

Before wearing the cowl:
	if the player is wearing the earphones:
		say "The helmet is normally a snug fit. You can't wear it with the earphones on." instead;

Rule for clarifying the parser's choice of the cowl:
	do nothing;

Understand "bulky/big/large/cumbersome/heavy/helmet/headgear/mask" or "hazmat helmet" as the cowl.

the visor is part of the cowl. Understand "visor/sunglasses/glasses" as the visor. The description of the visor is "[if the cowl is worn by the player]It obscures your vision a little, but it also hides your face[otherwise]The visor looks like rectangular sunglasses[end if].";



Book Hazmat Crew

Part Crew Themselves

The responders are people in Staging Area. The responders are plural-named.

The printed name of the responders is "the responders".

Rule for clarifying the parser's choice of the responders:
	do nothing;

Understand "emergency/crews/responder/Jessica/Dave" or "hazmat/emergency/crews" or "hazmat/emergency/crew" or "man" or "woman" as the responders. Understand "everyone" or "every one" or "everybody" or "every body" as the responders when the location is Staging Area.

The description of the responders is "[responder-description]."

To say responder-description:
	if the location is Staging Area:
		unless iPhone Deadly has happened:
			say "Like you, the responders are wearing hazmat suits. Now that they are uncowled, they look less otherworldly and more world-weary. Their faces are lined with concern and frustration";
		otherwise:
			say "The responders [if Unmasked is happening]look suspicious and angry[otherwise]edge away from you. To them, you are pestilence incarnate[end if]";


The responders wear hazmat-suits called responder suits. Understand "suits" as the responder suits.

The responders carry helmets called headgear. [The description of the helmet worn by the responders is "[description of helmet]."] The headgear are plural-named.

Understand "cowls" or "responders helmets" or "responders cowls" or "helmets" or "helmets of responders" or "cowls of responders" as the headgear. The printed name of the headgear is "responders['] helmets"

[Inform hack.]

[The stupids are scenery. The cowls are scenery.]
[
Understand "suits" as the stupids. Understand "helmets" as the cowls.

Every turn:
	now the stupids are in the location of the responders;
	now the cowls are in the location of the responders;

Instead of doing something to the stupids (called the S):
	say "yu";
	[let the present whim be the current action; 
	try the present whim;]

]




Part Dr Steve

Steve is a man in Staging Area. The printed name of Steve is "Dr. Steve". 

Understand "Dr Steve" or "Doctor Steve" or "Doctor" or "Dr" as Steve.

The description of Steve is "[if the Steve is alive]Dr. Steve looks even more worn out than the others. His bald head drips with sweat, although the gym is a comfortable temperature[otherwise if Steve is drained]Dr. Steve sleeps peacefully. He'll sleep eternally if you drain him again[otherwise]Dr. Steve is quite dead[end if]."

Book Ted

Ted is a man. Understand "man/guard/Ted/Tau/Teddy" as Ted. Understand "VP" or "vice president" or "veep" or "president" as Ted when ted-identity is familiar. 

When play begins:
	now Ted is zombie;
	now Ted is in Lobby;

ted-identity is a subject.

ted-zombie is a subject.

To say ted-zombie-familiar:
	now ted-zombie is familiar;

To say ted-or-corpse:
	if ted-zombie is unfamiliar:
		say "Ted";
	otherwise:
		say "the living corpse";
	
The description of Ted is "[if Ted is zombie]Ted is expressionless and pale, in contrast his picture on the badge dangling from his neck. He also wears a blazer, tie, and khakis, which don't seem out of place in this office, regardless of the plague outside.[paragraph break]He looks through you instead of at you. He appears to blink only when he remembers to do so[otherwise]Ted is motionless[end if]."

A getup is a kind of wearable thing.

Ted wears a getup called the blazer. 

When play begins:
	repeat with g running through getups:
		now the description of g is "This outfit is more than a boring fashion choice -- it's a corporate standard. [if g is Hal's outfit]Hal's button-up shirt is greased and spotted, and his tie is ragged red.[end if]" [All workers wear corporate outfits -- we don't want to spend a lot of effort on them.]

Understand "tie" or "blazer" or "jacket" or "khaki/pants" or "red/tie" or "outfit" as the blazer.

Before examining Ted's badge:
	now ted-identity is familiar;

Ted wears Ted's badge. The description of Ted's badge is "The badge shows a picture of a grinning, rosy-cheeked man with a caption that reads, 'Ted Tau, Vice President - Product Development, [company].'"

Understand "picture" or "photo" or "nametag" or "name tag" or "tag" or "caption" or "name badge" or "teds badge" as Ted's badge.

Book Margo

Margo is woman. Margo is in End of Corridor.

Margo can be hurt. Margo can be dangling. Margo can be struggling.


The description of Margo is "[if Margo is hurt]Margo's head is dented where you bashed it in. She hardly bleeds. [otherwise if Margo is dangling]Margo hangs from a ledge and is trying to pull herself up[otherwise if Margo is struggling]Margo is frantically trying to tear the earphones from her head[otherwise if Margo is dead]Margo is a mess of dried blood and twisted limbs, but she looks to be finally at peace[otherwise]According to the corporate badge she wears, this tall young woman in torn slacks and a stained blouse is marketing intern Margo Harris[end if].[margo-grasp]";

To say margo-grasp:
	if the player is on the chairs:
		say "[paragraph break]Her grasp on your arms is strong [unless chair-binding? is true] -- but you're sure you are stronger[end unless][period]";

Understand "blood" or "limbs" as Margo when Margo is dead.

Margo wears earplugs. The description of the earplugs is "Earplugs are jammed into Margo's ears." Understand "ears" or "ear plugs" or "earplug" or "ear plug" as the earplugs.

Before taking something held by Margo:
	say "[one of]You try to swipe the [noun], but Margo keeps it out of your reach.[paragraph break]'Nice try, Polly Dearest.'[or]There's no way Margo will let you.[stopping]" instead;

Understand "swipe" as taking.	

Understand "margo/harris" or "marketing/intern" as Margo 

When play begins:
	now Margo is zombie;
	
Margo wears Margo's badge. The description of Margo's badge is "Margo Harris' title is marketing intern. The unspoken message of her wide-eyed photo was that her first day of work was terrifying."

Understand "picture" or "photo" or "nametag" or "name tag" or "tag" or "caption" or "name badge" or "margos badge" as Margo's badge.

Margo wears a getup called Margo's outfit.

Understand "blouse" or "slacks" as Margo's outfit.

Book Hal

Hal is a man. Hal is in Upper Lobby.

Understand "Hal/Tau" or "man" as Hal.

The description of Hal is "[if Hal is dead][hal-dead-description][otherwise if hal is zombie][hal-zombie-description][otherwise if Hal is strong][hal-strong-description][otherwise][hal-regular-description][end if]."

To say hal-regular-description:
	say "[if False Climax First is happening]Hal holds your Walkman in shaking hands, unsure of what to do[otherwise if Musical Walkman is happening]Hal averts his eyes due to your suffering or his shame[otherwise]Hal is pale and seems to peer at your from the other side of a shadow.[paragraph break]Judging from how loose his button-up shirt and slacks are, he recently lost some weight. He wears a badge that has been chewed beyond readability[end if]";

To say hal-dead-description:
	say "Hal is almost peaceful and rather dead";
	
To say hal-zombie-description:
	say "Hal is devoid of any semblance of soul or free will";

To say hal-strong-description:
	say "A faint glow radiates from below Hal's tense muscles";

Understand "muscles" or "hand" or "hands" or "glow" or "expression" or "face" as Hal when Hal is strong.

Hal wears a getup called Hal's outfit.

Understand "shirt" or "tie" or "sleeve" as Hal's outfit.

Hal wears Hal's badge. The description of Hal's badge is "Hal's badge is a chunk of half-chewed plastic dangling from a lanyard."

Understand "nametag" or "name tag" or "tag" or "name badge" or "plastic" or "lanyard" or "hals badge" as Hal's badge.


[To be used in room descriptions.]
To say hal-room-desc:
	if Hal is in the location:
		unless Margo is in the location:
			say "[paragraph break][one of]As you study the [printed name of the location in lower case], Hal studies you back[or]Hal can't seem to decide if you are a friend or an unspeakable nightmare made flesh[or]Hal avoids your gaze[or]Hal looks away from you[or]In the scarlet lights, Hal could be half-demon[or]Hal has the wild-eyed look of a desperate man[or]Although music is playing and you clearly can't drain him, Hal appears fearful of your presence[in random order][period]";
		otherwise:
			say "[paragraph break][one of]Hal can't bring himself to look at Margo, who waves you onward[or]Hal keeps his eyes averted but his fist clenched[or]Hal appears to be on the verge of fainting[or]Hal's eyes are empty of every emotion but hatred[or]Hal silently begs you to do something[in random order][period]";
	otherwise:
		do nothing;


has-attempted-kill-hal? is truth state that varies.

Instead of attacking Hal when the player has the pipe:
	say "[one of]You lift the pipe above your head[period][if Steve is dead][paragraph break]How can you unsee Dr. Steve's lifeless face[question][end if][if denise-drain-attempt? is true][paragraph break]Maybe you are the stuff of nightmares, the giver of death. How can your forget your attempt to drain the half-dead girl on the stairs[question][end if][paragraph break]'Do it!' Hal whispers. 'Show me quick mercy, witch.' [paragraph break]You aren't ... this isn't who you are. Benny knew this.[paragraph break]Hal sighs at your loss of resolve, and he only looks a little relieved[if Margo is in the location]. Margo snickers at both of you[period] [end if][or]You can't bring yourself to do it[stopping].";
	now has-attempted-kill-hal? is true;


Book Denise

Denise is a witch. Denise is in Base.

Denise is privately-named.  [At first, we don't know Denise is the girl. We will fix this when we know Denise's identity (in Part Denise First).]

denise-name is a subject.

Every turn:
	if denise-name is unfamiliar:
		now the printed name of Denise is "the girl";
	otherwise:
		now the printed name of Denise is "Denise";

Understand "Denise" or "beat/witch" as Denise when denise-name is familiar.

Understand "little/child/girl" as Denise.

Denise can be comatose.

When play begins:
	now Denise is comatose;

The description of Denise is "The girl can't be more than 12. She wears a torn plaid skirt and stained blouse, but her golden curls are immaculate[if Denise is comatose]. Her perfectly symmetrical lips are pressed together in a pout[end if].";

Denise can be mad, glad, or neutral.

To say make-denise-glad:
	now Denise is glad;

To say make-denise-mad:
	now Denise is mad;

To say make-denise-neutral:
	now Denise is neutral;

When play begins:
	now Denise is neutral.

The curls are a thing. The curls are plural-named. They are part of Denise. The description of the curls is "Golden ringlets that highlight the perfection of the face." Understand "hair" or "ringlets" or "rings" or "golden/gold/curls" or "curl" as the curls.

Understand "face" or "girls face" or "girl face" or "lips"  or "features" or "smile" or "grin" or "pout" as Denise's face. The description of Denise's face is "Each of the girl's features is the ideal size and distance from the others."

Denise wears the skirt. The description of the skirt is "Her blouse and plaid skirt could have come from one of those old-fashioned boarding schools that only exist in movies nowadays."

Understand "outfit" or "skirt" or "blouse" or "clothes" as the skirt.

Volume All The Things

A thing can be examined.

Before examining something:
	now the noun is examined.

Instead of dropping something:
	if the noun is the notepad:
		say "You decide to keep the notepad. It's how you communicate." instead;
	otherwise if the noun is the Walkman:
		say "You cannot. The Walkman is your only weapon." instead;
	otherwise if the noun is a helmet enclosed by the player:
		say "You decide to hold onto the helmet. It may prove a useful disguise later, though it hasn't done you much good so far." instead;
	if the noun is the pipe:
		continue the action; [We won't have the pipe for long, so this not a big deal.]
	otherwise:		
		say "You may need [the noun]." instead;

Instead of taking off the uniform:
	say "You don't have time to undress.";


Book iPhone

The iPhone is a thing. The description of the iPhone is "The latest model, this crepe-thin iPhone packs a strong speaker.";

Understand "phone" or "speaker" or "lockscreen" or "lock screen" or "screen" as the iPhone.


Book Notepad


Understand "write" or "write [text]" or "write in [notepad]" or "write [text] in [notepad]" as a mistake ("You lost your pen on your way here. You're lucky you still have your life.") when the player carries the notepad.

The notepad is a thing. The player carries the notepad. 

Understand "note pad" or "notebook" or "note book" or "book" or "pad" or "paper" or "page" or "statements" or "statement" or "words" or "conversational" as the notepad.

Before showing the notepad to someone (called the looker):
	try talking to the looker instead;

Instead of talking to the player:
	say "You have no need to hold a committee meeting of one.";

Instead of opening the notepad:
	say "The notepad contains your prepared statements.[paragraph break]You should SHOW the notepad to someone for it to be of any use.";

The description of the notepad is "It's a pocket-sized notepad, and it's your only way to speak. To use it in conversation, try to SHOW it to someone."


Book Walkman

The Walkman is an openable transparent container. The Walkman is closed. 

The basic accessibility rule does nothing when the noun is the tape.

The examine containers rule does nothing when the noun is the Walkman.

The player carries the Walkman. Understand "tape recorder" or "recorder" or "stereo" or "player" or "cassette recorder" or "cassette player" or "tape player" as the Walkman.

The description of the Walkman is "It isn't actually a Walkman -- it's an off-brand portable tape player with 'Walkman' scrawled in white-out in your brother's handwriting. The player is [if the Walkman is open]open[otherwise]closed[end if], and the tape inside is set to [tape-side-description]." [[if the player has the Walkman][paragraph break]To operate the Walkman, simply type PLAY and STOP.[end if]";]

The earphones are a wearable thing. The earphones are plural-named. The earphones can be snapped.

The earphones are part of the Walkman. The description of the earphones is "These are the kind of earphones that go over the top of the head. They are discolored and have lost their fuzzy padding. They're linked to the tape player by a long cord that has rusted into the audio jack [if the player wears the earphones]You are wearing them[end if]."

Understand "ear phones" or "head phones" or "headphones" or "phones" or "cord" or "audio/jack" or "earphone cord" or "headphone cord" or "ear phone cord" or "head phone cord" or "wire" as the earphones.

The tape is a thing. The description of the tape is "An ancient tape cassette whose label has been written on and crossed out many times. It is on [tape-side-description]."

To say tape-side-description:
	say "side [if tape-side of the tape is A-side]A, which you labeled [side-a-name][otherwise]B, which you labeled [side-b-name][end if]";

To say side-a-name:
	say "'Straightjacket Tuba'";

To say side-b-name:
	say "'Benny'";


Understand "tape/cassette" as the tape.

The tape is in the Walkman.

Rule for deciding whether all includes the tape: it does not.

[
To say tape-eject:
	say "You try to eject the tape, but it has rusted shut inside the Walkman.";

Instead of opening the Walkman:
	say "[tape-eject]";

Instead of opening the tape:
	say "[tape-eject]";
	]

	
Before opening or closing the Walkman:
	if the player does not carry the noun:
		say "[walkman-error]" instead;
		
Before flipping the tape:
	if the player does not carry the Walkman:
		say "[walkman-error]" instead;

To say walkman-error:
	say "You aren't holding the Walkman.";

Understand "eject [something]" as opening.

Instead of opening the tape:
	try opening the Walkman.

Report opening the Walkman:
	say "The Walkman springs open." instead;


Report closing the Walkman:
	say "You click the Walkman shut." instead;

[
Understand "eject [tape]" as a mistake ("[eject-tape]");

To say eject-tape:
	try silently opening the Walkman;
	
]


Part Playing Functionality


Understand the command "switch" as something new.


Understand "listen to [walkman]" or "listen to [tape]" as switching on.

Instead of switching on something (called the playable):
	if the playable is the tape or the playable is the Walkman:
		try silently playing tunes;
	otherwise:
		continue the action;

Instead of switching off something (called the playable):
	if the playable is the tape or the playable is the Walkman:
		try silently stopping tunes;
	otherwise:
		continue the action;
		
Playing tunes is an action applying to nothing.

Understand "play" or "push play" as playing tunes.

currently-playing? is a truth state that varies.

Instead of playing tunes:
	if the player has the Walkman:[we don't want to be able to play the Walkman until further in the game]
		say "Don't forget your purpose here. The music in that Walkman is for one person only." instead;
	otherwise:
		say "[unless Hal holds the Walkman]Nothing to play[otherwise]You can't play the tape when Hal is holding the player.[end if]"; 
	[otherwise if the Walkman is open:
		say "(closing the Walkman)[line break]";
		now the Walkman is closed;
	say "You push play.";
	now currently-playing? is true.]

Playing is an action applying to one thing.

Definition: A thing is nontapy if it is not the tape.

Understand "play [something]" or "listen to [something]" or "listen to [something]" or "switch on [nontapy thing]" or "turn on [something]" or "switch [nontapy thing] on" or "turn [something] on" as playing.


Instead of playing something (called the playable):
	if the playable is not the Walkman and the playable is not the tape:
		say "[if the noun is the iphone]Already playing[otherwise]Unplayable[end if].";
	otherwise:
		try silently playing tunes.


Stopping tunes is an action applying to nothing. Understand "stop" as stopping tunes.

Definition: A room is iphone-steve if it encloses Steve and it encloses the iPhone.

Instead of stopping tunes:
	if currently-playing? is true and False Climax Unaction is happening:
		say "The Walkman's out of your reach.";
	otherwise if the location is iphone-steve:
		say "[if Steve is drained or Steve is dead]There's no way you'd be able to get close enough without being shot[end if].";
	otherwise if the iPhone is in the location:
		say "The phone appears to be locked.";
	otherwise:
		say "Nothing is currently playing!";

Understand the command "lock" as something new.

Understand the command "unlock" as something new.

leashing is an action applying to one thing. Understand "lock [something]" as leashing.

unleashing is an action applying to one thing. Understand "unlock [something]" as unleashing.
 

Before unleashing something:
	if the noun is the iPhone:
		say "You lack the passcode or fingerprints to do that." instead;
	otherwise:
		say "[if the player is in South Side and Polly is godly]You don't need to go inside the building anymore[otherwise if the player is in South Side]The doors are already unlocked[otherwise if the player is in Lobby]You don't control the unlocking mechanism[otherwise if the noun is the office]The office is already unlocked[otherwise]That would be a waste of time[end if].";

	
Before leashing something:
	if the noun is the iPhone:
		say "It's already on a lockscreen.";
	otherwise:
		say "[if the player is in South Side and Polly is godly]It's already locked[otherwise if the player is in South Side]You doubt you can lock the building from the outside[otherwise if the player is in Lobby]The doors are already locked[otherwise if the noun is the office]This office can't be locked[otherwise]That would be a waste of time[end if].";
		[say "[if the noun is the double doors and the player is in the Lobby]The doors are already locked, fortunately[otherwise if the noun is the double doors and Resurrection is not happening]They are already lockedYou don't need to unlock [noun][end if]."]


Stopping is an action applying to one thing.

Understand "stop [something]" or "stop [something]" or "switch off [something]" or "turn off [something]" or "switch [something] off" or "turn [something] off" as stopping.

Understand "switch [Walkman]" as stopping [when currently-playing? is true].

Understand "switch [Walkman]" as playing [when currently-playing? is false].

Instead of stopping the tape:
	try silently stopping tunes;

Instead of stopping the Walkman:
	try silently stopping tunes;

Instead of stopping something:
	say "Everything will eventually stop.";

tape-side is a kind of value. The tape-sides are A-side and B-side.

The tape has a tape-side. The tape-side of the tape is A-side.

Flipping is an action applying to one thing. Understand "flip [something]" or "turn [something] over" or "turn over [something]" or "turn [something]" or "switch [tape]" as flipping.


Before flipping the tape:
	if the Walkman is closed:
		now the Walkman is open;
		say "(opening Walkman)";

Instead of flipping something (called the flipper):
	if the flipper is the tape:
		if the tape-side of the tape is A-side:
			now the tape-side of the tape is B-side;
		otherwise:
			now the tape-side of the tape is A-side;
		say "You switch the tape to the [tape-side of the tape].";
	otherwise:
		say "You decide it's best not to treat [the noun] like a cassette tape.";

Instead of taking the tape:
	say "You should leave it in the Walkman. Losing it would be a disaster.";


Before taking the tape when the tape is in the Walkman:
	[if Musical Walkman is happening:
		try silently stopping tunes;
		say "(stopping the music)";]
	if the Walkman is closed:
		say "(opening the Walkman)";
		now the Walkman is open;
	
Part Earphone Rules


The can't take component parts rule does nothing when wearing the earphones.

Before wearing the earphones:
	[ignore the can't take component parts rule;]
	if the player wears a helmet:
		say "You can't wear the earphones while wearing the helmet." instead;
	say "You slip on the earphones[if a musical scene is happening]. They do nothing to block out the music[end if].";
	now the player wears the earphones instead;

Definition: A person is another if it is not the player.

The can't take off what's not worn rule does nothing when taking off the earphones.

[Before taking off the earphones:
	if the player is not wearing the earphones:
[		ignore the can't take off what's not worn rule;]
		try silently taking the earphones instead;][Not sure what I was thinking here. Do we need this?]


Before taking the earphones (this is the earphone-taking rule):
	if the earphones are worn by an other person (called character) and False Climax is happening:
		[now the earphones are part of the Walkman;]
		say "[one of]'What are you doing?' Hal says. '[if hal-yes? is true]You told me you were here to kill her[otherwise]I don't care what you told me back there -- I know you're here to kill her -- now do it[end if][exclamation][or]Hal's right -- Margo wearing the earphones are part of your plan.[stopping]'" instead;
	otherwise:
		say "The earphones have rusted to the Walkman." instead;


Before putting the earphones on a person (called the character):
	if the character is Margo and False Climax is happening:
		if Margo is wearing the earphones:
			say "[Character] is already wearing the earphones.";
		otherwise:
			now dont-give-room-description? is true;
			try exiting;
			follow the eliminate-room-description rule;
			now dont-give-room-description? is false;
			now the character wears the earphones;
			say "You leap from your chair and put the earphones on Margo. Hal presses play.[paragraph break]She claws at your hands as you press the earphones tight against her head.";
			now Margo wears the earphones instead;


Understand "fast forward" or "ff" or "fastforward" or "rewind" as a mistake ("Only the play and stop buttons work. The Walkman will automatically rewind and start over when it reaches the end of the tape.");

The batteries are part of the Walkman. Instead of doing something to the batteries, say "You managed to find fresh batteries for the Walkman before you arrived. You don't need to worry about them today."

Understand "battery" as the batteries.

Part Benny's Message
	
To say benny-intro:
	say "[italic type]Polly, I'm not forgiving you for what you did today. But Dad giving you up to your executioners -- I can't stand it, and it's not fair.[roman type]";

To say benny-full:
	say "[italic type]Polly, I'm not forgiving you for what you did today. But Dad giving you up to your executioners -- it's not right.
	
	As soon as I finish this recording, I'm going to free you from the basement. 
	
	I don't know how long you can run, Polly, but run. Stay away from everyone. Hide in alleys, in forests, in deserts. It's only safe in the quiet. 
	
	Maybe one day you can come home again, when everyone sees you're not some angel of death. But I think this will only happen when you do something so magnificent, no one can deny what you are. 
	
	When you don't need to run anymore, find me. 
	
	I won't be at the house. How can I live with the man who calls himself Dad but condemns you to death without a tear? 
	
	You know I can't go with you, though. I'd like to say it's easier for one to disappear than two together, but we both know it's dangerous for me to be nearby until you figure out the draining thing.
	
	Remember the times we had. I know you can't listen to us play [roman type]Straightjacket Tuba[italic type] anymore, but don't ever record over the other side of this tape. A bit of us is on there.

	Find me when it's safe. 
	
	Bye, Polly.[roman type]";



Volume Inventory Display	
[
After printing the name of the Walkman while taking inventory:
	if the earphones are worn by a person (called character):
		say " (earphones worn by [if the earphones are worn by the player]you[otherwise][character][end if])";
	if the earphones are not worn by a person:
		say " (earphones not worn)";]


Instead of taking inventory:
	say "[if the player has the notepad or the player has the walkman]Your arsenal is limited[otherwise]Your inventory is pitiful[end if].[paragraph break][have-notepad][have-walkman][have-pipe][have-shard] You are sweating under a yellow hazmat suit[helmet-wearing]."


To say have-shard:
	if the player has the shard:
		now has-shard is familiar;
		say " A jagged shard from when Margo broke the window is in your lap.[line break]"

To say have-notepad:
	if the player has the notepad:
		say " You have your conversational notepad.[line break]";

To say have-walkman:
	if the player has the walkman:
		say " Your most prized possession is [if the Walkman is examined]an imitation[otherwise]a[end if] Walkman ([if the Walkman is open]open[otherwise]closed[end if] and [if currently-playing? is true]playing[otherwise]silent[end if]), which is attached to earphones[if the earphones are worn by a person][earphone-wearing-person][otherwise][end if].[line break]";


To say have-pipe:
	if the player has the pipe:
		say "You clench a steel pipe.[line break]";

Definition: A person is earphoned if it is wearing the earphones.


To say helmet-wearing:
	if the player has the cowl:
		say " and ";
		if the player is wearing the cowl:
			say "are wearing a ";
		otherwise:
			say "are carrying a ";
		say "bulky helmet";
		

To say earphone-wearing-person:
	if a person (called the character) is wearing the earphones:
		if the character is the player:
			say " you are wearing";
		otherwise:
			say "  worn by [character]";
	


Volume Conversation

[Vorple does not hide its prompt when we have a menu. This should fix it.]

[vorple.prompt.unhide() and vorple.prompt.hide()

or hide the prompt and unhide the prompt

]

Section - New readchoice I6 routine 
 
[(in place of Section - Readchoice I6 routine in Simple Chat by Mark Tilford)]

[Include (-
[ ReadVorpleChoice low high i;
  for (::) {
	print "";
	KeyboardPrimitive (buffer, parse);
	i = TryNumber (1);
	if (i >= low && i <= high) return i;
	else print "[Please answer by typing a number between ", low, " and ", high, ".]^";
  }
];
-).

To decide what number is the choice made from (low value - a number) to (high value - a number) when Vorple is supported: (- ReadVorpleChoice ( {low value}, {high value} ) -);
]


Book Verbs

Talking to is an action applying to one visible thing.

Understand "talk to [something]" as talking to.

Understand "talk to [something]" or "t to [something]" or "t [something]" or "speak to [something]" or "communicate with [something]" or "say to [something]" or "converse with [something]" or "hello [something]" or "[something] hello" or "hi [something]" or "[something] hi" or "hey [something]" or "[something] hey" or "greet [something]" as talking to.


Definition: A thing is nonperson if it is not a person.

Instead of talking to a nonperson thing:
	say "You don't see any use in talking to [the noun].";

talker is a person that varies.

Before talking to someone:
	now talker is the noun;

Definition: A person is other if it is not the player.

Carry out talking to someone:
	if talker is other:
		if the player carries the notepad:
			show notebook;
		otherwise:
			say "With the notepad gone, you no longer have a voice." instead;


Every turn:
	now talker is the player;
	
Book Specific Conversations

When play begins:
	forbid exiting on zero;

Part Polly's Unaltered Notebook

show-notebook, introduce-self, mission-explanation, [join-circle,] stop-music, and have-compassion, and close-notepad are chat nodes.

Report giving link to a chat node (called c):
	if talker is Hal:
		if c is a menu listed in the Table of Altered Chat Links:
			say the message corresponding to a menu of c in the Table of Altered Chat Links instead;
	otherwise if c is a menu listed in the Table of Chat Links:
		say the message corresponding to a menu of c in the Table of Chat Links instead;

Table of Chat Links
Menu	Message
introduce-self	"Hello. My name is Polly."
mission-explanation	"If you do not cooperate, more will die."
[join-circle	"'I beg you to end this suffering.'"]
stop-music	"Stop the music, please. It hurts me."
have-compassion	"You must end this suffering. Have mercy."
close-notepad	"(close notepad)"


showing-notebook? is a truth state that varies;

To show notebook:
	if a musical scene is happening:
		say "You can't focus on your notepad right now." instead;
	if Meeting is happening or Entire Game is happening for 1 turn:
		say "Attempting to communicate with others would give you away.";
	otherwise if iPhone Deadly is happening:
		say "[no-one-cares]";
	otherwise if Unmasked is happening:
		if the talker is the responders:
			say "Not only is Dr. Steve the one in charge, he's also blocking your exit. You decide to talk to him instead.";
			now the talker is Steve;
			now the noun is Steve;
		now showing-notebook? is true;
		follow the scene changing rules;
		run a conversation from show-notebook;
		now showing-notebook? is false;
	otherwise if the noun is Ted:
		now talker is Ted;
		run a conversation from show-notebook;
	otherwise if the noun is Hal:
		if Hal is alive and currently-playing? is false:
			now talker is Hal;
			run a conversation from show-notebook-alt; [Denise changed the answers.]
		otherwise if Hal is drained:
			say "The notepad doesn't mean anything to Hal right now. Nor does anything else.";
		otherwise: 
			say "[action-talk].";
	otherwise if the noun is Margo:
		if False Climax is happening:
			if chair-binding? is true:
				say "[one of]'Hal's the one you should show the notepad to, not me, [polly-name],' Margo says[or]Margo's not the one you should be talking to[stopping].";
			otherwise:
				say "Margo doesn't care what you have to say.";
		otherwise:
			say "[one of]'Not now, [polly-name].'[or]Margo isn't interested in your notepad at the moment.[stopping]";
	otherwise if Bloodthirsty Mob is happening and the talker is the responders:
		say "[no-one-cares]";
	otherwise:
		say "You have nothing to say to [the talker]."; [We should never reach this point.]


To say no-one-cares:
	say "None of them cares about your conversational notepad. They see only the monster before them.";

notepad-exist is a subject.
	
Before taking inventory:
	now notepad-exist is familiar;	


Report giving text for show-notebook:
	say "[one of]You prepared [if notepad-exist is familiar]the[otherwise]a[end if] notepad to be your voice for any number of situations you might encounter here. [or][stopping]Which page do you show [if the talker is Denise]the girl[otherwise][the talker][end if]?" instead;

Carry out finding responses to show-notebook:
	link to introduce-self;
	link to mission-explanation;
	[link to join-circle;]
	link to stop-music;
	link to have-compassion;
	link to close-notepad;

Report giving text for a chat node (called N):
[	say "[N][line break]";
]	[if N is close-notepad:
		say "You close the notepad." instead;]
	if talker is Steve:
		if N is a chat listed in the Table of Steve Talk:
			say "[message corresponding to a chat of N in the Table of Steve Talk][if a paragraph break is pending][line break][end if]"; 
			follow the scene changing rules instead;
	otherwise if talker is Ted:
		say "Ted [one of]gives you a deeply empty stare[or]nods and goes back to gazing into nothingness[or]doesn[']t see the notepad -- or you, for that matter[or]doesn[']t seem to care -- about anything[if Witch Intro is happening] except the girl[otherwise], in fact[end if][in random order]." instead;
	otherwise if talker is Margo:
		say "[one of]'Talking by notepad?' Margo says. 'What are you, a toddler playing spy? Cute, but I'm cuter.'[or]Margo refuses to even look at your notepad.[stopping]";
	otherwise if talker is Hal:
		if False Climax First is happening:
			if N is a chat listed in the Table of Hal Talk			:
				say "[message corresponding to a chat of N in the Table of Hal Talk][if a paragraph break is pending][line break][end if]" instead;
	otherwise if talker is Denise:
		if Denise is comatose:
			say "Her eyes are closed, so she misses out on the conversation." instead;
		otherwise if Denise First has ended:
			say "[if Denise is mad]'That game is over, stink face[otherwise]'Stop interrupting[end if].'" instead;
		[otherwise:
			if N is a chat listed in the Table of Hal Talk:
				say "[message corresponding to a chat of N in the Table of Hal Talk][if a paragraph break is pending][line break][end if]" instead;]
		[otherwise:
			say "The girl yanks the notepad out of your hands.[paragraph break]'[italic type]This[roman type] is how you talk?' she asks through Ted. She thumbs through the pages. 'Let's get this all over at once. Let's see ...[paragraph break]'Hi, Polly, I'm Denise.[paragraph break]'People are [italic type]supposed[roman type] to die. Ask any librarian.[paragraph break]'No music here. Nasty stuff.[paragraph break]'And -- good news! -- I'm about to end this suffering.'";
			now denise-name is familiar instead; [We now know to refer to her as Denise, not the girl.]]

To say action-talk:
	say "Now's the time for action, not talk";

Table of Steve Talk
chat	message
introduce-self	"'People here call me Dr. Steve. I'm afraid you're in quite a bit of trouble, young lady. This is a quarantined area, and --'"
mission-explanation	"Dr. Steve scowls.[paragraph break]'Was that a threat?'"
stop-music	"'What music?' Dr. Steve asks. 'The only kind of person music can harm is --'"
have-compassion	"'Suffering? It's nothing personal,' Dr. Steve says, unaware that page was not meant for him. 'You are unauthorized and in a quarantined area. We have to report you.'"
close-notepad	"You close the notepad. They won't listen anyway."

	
Part Polly's Altered Notebook

show-notebook-alt, introduce-self-alt, mission-explanation-alt, stop-music-alt, have-compassion-alt, and close-notepad-alt are chat nodes.

Table of Altered Chat Links
Menu	Message
introduce-self-alt	"Hello. My name is Poophead."
mission-explanation-alt	"I want to die. Will you cooperate?"
stop-music-alt	"Start the music, please. I crave pain."
have-compassion-alt	"Your suffering is deserved, and your death will be a mercy." 
close-notepad-alt	"(close notepad -- you're not playing this stupid game)"

is-poophead? is a truth state that varies.

To say set poophead:
	now is-poophead? is true;

To say polly-name:
	if is-poophead? is true:
		say "Poophead";
	otherwise:
		say "Polly";


Carry out finding responses to show-notebook-alt:
	link to introduce-self-alt;
	link to mission-explanation-alt;
	link to stop-music-alt;
	link to have-compassion-alt;
	link to close-notepad-alt;


Report giving text for show-notebook-alt:
	say "[one of]Your words have been altered in purple crayon. Margo snickers behind you[or]None of the heavily edited choices in your notepad look good[stopping]." instead;


[Table of Denise Talk
chat	message
introduce-self-alt	"[set poophead]Denise giggles through Ted. 'I like you, [polly-name].'[make-denise-glad]"
mission-explanation-alt	"Denise claps.[paragraph break]'Maybe I was wrong about you. You're all right, [polly-name].'[make-denise-glad]"
stop-music-alt	"'Is that why you sought me out?' Denise asks. 'Why should you be hunted because of your gifts?" [We're leaving Denise neutral.]
have-compassion-alt	"'So tickled you agree!' Denise and Ted clap their hands at the same time. 'It's hard to meet anyone who's as right as I am.'[make-denise-glad]"
close-notepad-alt	"You close the notepad.[paragraph break]Denise scowls at your refusal to play her game.[make-denise-mad]" ]

Table of Hal Talk
chat	message
introduce-self-alt	"[one of][set poophead]'I'm afraid I don't understand, Polly,' Hal says.[paragraph break]'That's [polly-name] to you, sweetheart,' Margo says[or]Hal shakes his head. Even for a beat witch, you are strange[stopping]."
mission-explanation-alt	"[hal-start-music]But if you insist ...'[paragraph break][start-music]"
stop-music-alt	"[hal-start-music]I'll tell you this much: Pain is overrated.'[paragraph break][start-music]"
have-compassion-alt	"Hal reddens. His voice is soft and sharp. 'I lead a multi-million dollar company. I have been featured on dozens of trade magazine covers and have three honorary doctorates. What have you accomplished besides hiding? You have no right to talk to me like that, witch.'[paragraph break][start-music]"
close-notepad-alt	"You close the notepad.[paragraph break][one of]'Oh, come on, [polly-name],' Margo says. 'You're such a wet-blankie spoilsport.' [or]Margo sighs in disappointment.[stopping]" 


To say start-music:
	say "Click. The tape whirs to a start[period][line break]";
	now currently-playing? is true;
	follow the scene changing rules;

To say hal-start-music:
	say "'I don't want to do this. I really don't,' Hal says[period] '";




Volume Magic


Book Living States


[Every person has a number called lifenum.

The lifenum of a person is usually 2.
	
Every turn:
	repeat with P running through persons:
		if the lifenum of P is at most 0:
			now the lifenum of P is 1;
	repeat with P running through nonwitch persons:
		if the lifenum of P is at least 3:
			now the lifenum of P is 3;
	repeat with P running through witches:
		if the lifenum of P is at least 4:
			now the lifenum of P is 4;]


A thing can be nonliving, alive, dead, dying, zombie, wounded, drained, uncomfortable, strong, stronger, demi-godly, or godly. A thing is usually nonliving. A person is usually alive. 

Definition: A person is buff if it is strong or it is stronger or it is godly or it is demi-godly.


Instead of the player doing something to a dead person:
	if the current action is examining or giving life to or taking life from:
		continue the action;
	otherwise:
		say "You can do nothing for the dead.";
		

Instead of taking something enclosed by a dead person:
	say "[one of]You move to take [the noun] but stop when your conscience and self-disgust get too much for you[or]You refuse to disrespect the dead[stopping].";
	


[Every person has a number called health.

The health of a person is usually 3.

Every turn:
	repeat with P running through persons:
		if the health of P is at most 0:
			now P is dead;
	repeat with P running through nonwitch persons:
		if the health of P is at least 3:
			now the health of P is 3;
	repeat with P running through witches:
		if the health of P is at least 4:
			now P is godly;]
			

Part Wounded

blood is scenery. The description of the blood is "Blood flows freely from your leg. [if the location is in City]It leaves a splattered trail that glistens maroon in the light of the body fires behind you. There's no way your pursuers will miss it.[end if]"

Understand "path/trail of blood" or "path/trail" as the blood when the location is in City.

Every turn when the player is wounded:
	if the blood is not in the location:
		now the blood is in the location;

[Book Mouthpiece
	
A person can be mouthpiece.
	
Ted is mouthpiece.

Definition: A room is undenise if it does not enclose Denise.

Understand "[Denise]" as a mouthpiece person when the location is undenise.

Instead of doing something to a mouthpiece person when the location is undenise:
	if the current action is talking to:
		continue the action;
	otherwise:
		say "You can TALK TO DENISE in the presence of one of her corpses. But she's not actually here.";]

	
Volume Scenes

Book Music Setup

Part Basic Musical Scenes

Understand "eat music" as a mistake ("That's just an expression.").

countdown-clock is a number that varies. 

A scene can be musical or semi-musical or silent. A scene is usually silent.

A scene has some text called playing-text.

music-harm? is a truth state that varies. music-harm? is usually true.

When a musical scene ends:
	now music-harm? is false;

Every turn when a musical scene is happening:	
	if the music is not in the location:
		now the music is in the location;
	if music-harm? is true:
		increase countdown-clock by 1;


Part Music Description

Twilight Zone is a room.


the music is scenery in Twilight Zone.

Understand "beat" or "elevator music" or "notes" or "melody" or "harmony" or "rhythm" as the music.


[Current musical scenes: Bloodthirsty Mob, Explore, Musical Walkman, and Deadly iPhone. Not sure if going to keep Musical Walkman.]

To say steve-techno:
	say "The iPhone is playing a lively techno dance tune" [We'll also use this same music at the end.]

To say doesnt-affect:
	say "It doesn't affect you at all";

Table of Music Descriptions
Scene	Description
iPhone Deaf	"You can barely hear the beat of the music. [doesnt-affect]."
Deadly iPhone	"[steve-techno]."
Bloodthirsty Mob	"The music is a harmless whisper. [doesnt-affect]."
Torture	"The elevator music is just loud enough to crucify your mind but not enough to kill you."
Musical Walkman	"You and Benny created this song and spent two weeks putting all the tracks together in the corner of the garage you turned into a recording studio." 
Margo's Music	"You can barely hear the tuba and harp playing."

Definition: A scene is song if it is not silent. [That is, if it's musical (harmful) or semi-musical (music faint enough not to harm or weaken). ]

When a song scene begins:
	set musical descriptions;

Every turn when a song scene is happening:
	set musical descriptions;

To set musical descriptions:
	if a song scene (called song-scene) is happening:
		repeat through the Table of Music Descriptions:
			if song-scene is the scene entry:
				now the description of the music is the description entry; 

Instead of doing something to the music:
	if the current action is touching:
		say "Unfortunately, music tends to touch you.";
	otherwise if the current action is listening to or examining:
		continue the action;
	otherwise:
		say "You can't.";

Before listening to the music:
	try examining the noun instead;

This is the music-follows-you rule:
	if the music is not in the location:
		now the music is in the location; 

When a song scene begins:
	follow the music-follows-you rule;
	
 Every turn when a song scene is happening or iPhone Deaf is happening:
	follow the music-follows-you rule;



When a musical scene ends:
	now the music is in Twilight Zone;


a thing can be groovy.

The Walkman is groovy. The iPhone is groovy. The earphones are groovy.

Before listening to a groovy thing when a musical scene is happening or a semi-musical scene is happening:
	try listening to the music instead;

Book Story

Part First

When play begins:
	say "The emergency responders move like yellow ghosts through smoke-filled streets.[paragraph break]The group banded together before you could get away. Now you find yourself in the middle, an unwilling participant in a silent parade that marches past cars that won't start and shops that are eternally closed.[paragraph break]In the darkness, you seem like the others. You even adopt their attitude of ignoring the piles of smoldering dead.[paragraph break]The group stops near a high school gymnasium. One by one, you move past the electric generators, through plastic sheets, and into the light of the staging area.[paragraph break]When the last of you has stepped through the final veil, the door seals, and the scrubbers whir. [paragraph break]The one in charge -- Dr. Steve, you believe everyone calls him -- takes off his hazmat helmet and motions everyone to do the same.[paragraph break]You're done for.";


Part Meeting

dave-subject is a subject.

take-off-headgear-subject is a subject.

headgear-attempts is a number that varies. headgear-attempts is 0.

To say comatose-dead:
	say "despite Dr. Steve's[if Steve is drained], um, permanent[otherwise]comatose[end if] state";

Instead of taking off the cowl when take-off-headgear-subject is unfamiliar and the player is wearing the cowl:
	if iPhone Deadly has happened and the location is Staging Area:
		say "The iPhone is still going [if Steve is drained or Steve is dead][comatose-dead][end if]. You would rather put your head in a fire than expose yourself to music again." instead;
	increase headgear-attempts by 1;
	if headgear-attempts is 1:
		say "You reach up to your helmet and hesitate. Maybe you can get away with looking comfortable with the helmet on.";
	otherwise:
		say "No sense in postponing the inevitable.";
		continue the action;

Report taking off the helmet when Meeting is happening:
	say "[if headgear-attempts is at most 0]You have no choice. [end if]You sigh and remove the helmet." instead;

To say steve-directions:
	[say "[paragraph break]Dr. Steve wipes his brow, gestures to the tables, and clears his throat.[paragraph break]'My pathology team has not found the source of the disease,' he announces.[paragraph break]The news is a surprise to no one, but everyone's shoulders slump.[paragraph break]You also feel desperation and despair, but for a different reason -- Dr. Steve is standing between you and the door you entered, which is now magnetically sealed.[run paragraph on]";]
	say "[paragraph break]Dr. Steve wipes his brow, gestures to the tables, and clears his throat.[paragraph break]You can only half-hear with this bulky helmet on your head.[paragraph break]' ... pathology team ... not found ... source ... disease,' he announces.[paragraph break]The news is a surprise to no one, but shoulders slump.[paragraph break]You're feeling despair for a different reason -- Dr. Steve is standing between you and the door.[run paragraph on]";

Meeting is a scene. Meeting begins when Entire Game is happening for two turns.	


Every turn when Entire Game is happening for one turn:
	[say "'We have a few more tests to run.' Dr. Steve tries to sound encouraging but fails. 'What about your crew, Jessica?'[paragraph break]'We're disposing of the bodies as directed,' Jessica says. Her voice is quivering, but she forces the report from her lips. 'Still ... I don't think anyone at home knew the scale. I need a bigger crew -- hundreds more. We can only focus on the streets and parks. We're leaving anything indoors alone for now.'[paragraph break]'I don't think anyone understood the numbers. I'll place the personnel order when the meeting's over. Dave?'";]
	say "'... few more tests ...' Dr. Steve is trying to sound encouraging. '... Jessica?'[paragraph break]'... disposing of the bodies ...' Jessica says. Her voice is quivering, but she forces the report from her lips. '... didn't know the scale. With all ... need ... bigger crew ... can only focus ... streets ... parks ... can't ... anything indoors ...'[paragraph break]Dr. Steve nods.[paragraph break]'No one understood ... sheer numbers of deceased ... will radio ... more personnel ... Dave?'";
	now dave-subject is familiar;
	
Every turn when Meeting is happening for one turn:
	[say "Dave mutters something incoherent.[paragraph break]'Dave?'[paragraph break]He doesn't look up. 'No riots, no looting.'[paragraph break]'And ...?'[paragraph break]'No criminals. No people. No parents. No children. Just ... silence.'[paragraph break]He looks down again.";]
	say "Dave mutters something you probably wouldn't have been able to hear, even if your helmet were off.[paragraph break]'Dave?'[paragraph break]He doesn't look up. '... riots ... looting.'[paragraph break]'None?'[paragraph break]Dave's voice rises and cracks.[paragraph break]'No panic ... people ... parents ... children. Just ...' He trails off into what could be swearing or a prayer. '... silence. No one.'[paragraph break]He looks down again.";
	
Every turn when Meeting is happening for two turns:
	[say "'Well, I'm seeing the same thing as you all, and -- Alisha, it's safe to remove your headgear,' Dr. Steve says.[paragraph break]You jolt. He's looking at you.";]
	say "'I'm seeing the same ...  and Alisha ... safe ... can take off ...'[paragraph break]At first you think Dr. Steve's pretending to pop off his head, but he's actually demonstrating how to remove the headgear.[paragraph break]You jolt. He's looking at you.[paragraph break][italic type]Everyone[roman type] is.";
	now take-off-headgear-subject is familiar;


Meeting ends when the player carries the cowl. [isn't wearing it]

Every turn when Meeting is happening for three turns:
	say "'What's ... matter with ... Alisha?' Dr. Steve asks. 'Alisha?'[paragraph break]'Let me,' Dave says.[paragraph break]He yanks the helmet off your head.[paragraph break]You grab the helmet back, but it's too late.";
	now the player carries the cowl;

Part Unmasked

Unmasked is a scene. Unmasked begins when Meeting ends.

The pistol is a thing. The description of the pistol is "The hazmat crew apparently brought a few weapons in case they ran into trouble. They found it."

Understand "gun/weapon/handgun" as the pistol.

When Unmasked begins:
	say "More than one person yells in surprise. [if dave-subject is familiar]Dave[otherwise]One of them[end if] draws a pistol.[paragraph break]'Who are you, and what did you do with Alisha?' Dr. Steve demands. 'TALK TO me!'";
	now the responders hold the pistol;

Instead of wearing the cowl during Unmasked:
	say "[one of]You try to put the helmet back on your head, but someone behind you yanks your arms down. You're not getting out of this that easily[or]There's no sense in putting your disguise back on -- they already know you're not Alisha[stopping].";

Unmasked ends when showing-notebook? is true. 

Unmasked ends passively when Unmasked is happening for three turns.

[When Unmasked ends:
	say "Unmasked ends."]

Part iPhone Deadly

iPhone Deadly is a scene. iPhone Deadly begins when Unmasked has ended and the current action is not talking to.

iphone-deadly? is a truth state that varies.

When iPhone Deadly begins:
	say "The color leaves Dr. Steve's cheeks, and his eyes narrow. He diagnosed the situation faster than you expected. Woman with a perfectly proportioned face, can't speak, appears unexpectedly amid death ... it makes sense.[paragraph break]'Beat witch!' he shouts.[paragraph break]The others cry out in fear and press against the walls, as though you were a bomb. The now-trembling gun is still aimed at you.[paragraph break]Dr. Steve fumbles in his pockets and produces an iPhone. He can't swipe fast enough.[paragraph break]'Eat music, witch!'[paragraph break]And it starts.[paragraph break]";
	press any key;
	say "It happens to be a dance tune, it's on full blast, and it has no lyrics to soften the musical blow.[paragraph break]You collapse. Your entire body shakes in painful spasms.";
	now Steve carries the iPhone;

Every turn during iPhone Deadly:
	if countdown-clock is 1:
		say "The melody burrows into your mind in fiery needles. Your back arches, and you shriek soundlessly.[paragraph break]'Not to worry, everyone,' Dr. Steve says. 'Observe the subject's loss of balance, the dry-heaving, the drooping gaze. Yes, we are witnessing the beginning of a beautiful aneurysm.'";
	otherwise if countdown-clock is 2:
		say "The world crashes into itself like a whirlpool.[paragraph break]Every note you hear brings you one beat closer to your brain bursting. You press the helmet closer to your chest.";
	otherwise if countdown-clock is 3:
		say "Your head is a pain-spewing fountain. If you can't find a way to block out the sound, you're not going to last any longer.";
	otherwise if countdown-clock is 4:
		say "Fluid dribbles from your ears. Your last thoughts are a hallucination of musical notes ripping flesh off your bones.[paragraph break]The helmet drops from your unfeeling fingers. People cheer at the dead monster.";


Every turn:
	if Steve carries the iPhone and the cowl is not worn by the player and the location is Staging Area:
		now iphone-deadly? is true;
	otherwise:
		now iphone-deadly? is false;

[iPhone Deadly ends when the location is not the Staging Area.]


iPhone Deadly ends when the player is wearing the cowl.

Part Escape

Escape is a scene.

Escape begins when the player is strong and the player is in Staging Area.

When Escape begins:
	say "You are strong enough to deal with the door.";

	
Instead of attacking the magnet-door when the player is strong or the player is stronger:
	say "You give the magnetically sealed door a superhuman punch. It squeals and shoots into the street. You run outside.[paragraph break]Although a cheetah would have a tough time keeping up with you and you're still wearing the helmet, you can hear the panicked shouts behind you.[paragraph break]'Stop staring! [italic type]Shoot her![roman type]'[paragraph break]";
	now the player is in Street;
	press any key;
	say "A sudden numbness engulfs your leg. You careen into a school-crossing sign and barely catch yourself from falling. You gulp air and force yourself to move.[paragraph break]Your augmented strength wears off faster than usual and ebbs into weakness. Running slows to limping. You are a delirious rat in an asphalt maze.[paragraph break]You've never been to this city before, and you have lost your sense of direction. But you feel an itching, a howling in your mind that crescendos as you get closer to the other beat witch.[paragraph break][street-description]";
	now the player is wounded;


Escape ends when the player is in Street.

Part Running

Running is a scene. Running begins when Escape ends.

When Running begins:
	say "You note with dismay that you have left a trail of blood behind you. So much for losing this mob.";
	now the player is wounded;

Running ends when the player is in Lobby.


Part Threshold

Threshold is a scene. Threshold begins when the location is South Side.


When Threshold begins:
	now the cowl is in South Side;

Instead of taking the cowl during Threshold:
	say "[one of]You try to take it, if only not to leave another clue for those who now hunt you. But it's[or]It's[stopping] too heavy for you to lift now. You can barely walk.";


Threshold ends when Running ends.

Part Bloodthirsty Mob

Bloodthirsty Mob is a semi-musical scene. Bloodthirsty Mob begins when the location is Lobby.

When Bloodthirsty Mob begins:
	say "A pale man in a blue blazer and bright red tie sits at the desk. His name is Ted Tau, if his badge is to be trusted. He motions you farther in.[paragraph break]As soon as you clear the doorway, he pushes something under the desk, and the doors slam shut behind you.[paragraph break]Just in time too -- the first responders have found you.";
	now the responders are in South Side;
	now the responders carry the iPhone;
	now the description of the responders is "Although none of the responders wear their headgear, you can't help but see them as faceless nightmares.";
	now the double doors are closed;

Understand "faceless/nightmares/mob" as the responders when Bloodthirsty Mob has happened.

Understand "woman" as the responders when Bloodthirsty Mob has happened for three turns.

Understand "man" as the responders when Bloodthirsty Mob has happened for six turns.

Does the player mean doing something to the responders:
	it is likely; 
	[if Bloodthirsty Mob is happening:
		it is very likely;]

iphone-humming? is a truth state that varies.	

Every turn when Bloodthirsty Mob is happening:
	if Bloodthirsty Mob is happening for one turn:
		say "They rattle the double doors. Fists pound on the thick glass, but the doors hold.[paragraph break]Booted feet kick your helmet down the sidewalk, as though being a plague on humanity were contagious.[paragraph break]One of the responders waves Dr. Steve's phone at you. From the other side, you only hear a harmless hum.";
		now iphone-humming? is true;
	if Bloodthirsty Mob is happening for three turns:
		say "The woman presses the iPhone against the glass, but you still can't make out the music.";
	if Bloodthirsty Mob is happening for five turns:
		say "The woman shakes her head and lowers the iPhone.";
		now iphone-humming? is false;
	if Bloodthirsty Mob is happening for six turns:
		say "Dave motions the others to step back from the building.";
	if Bloodthirsty Mob is happening for seven turns:
		say "Dave aims the pistol at the doors. Three faint pops rattle the lobby.";
	if Bloodthirsty Mob is happening for eight turns:
		say "The others inspect the doors, but the bullets didn't even dent them.";
	if Bloodthirsty Mob is happening for nine turns:
		say "The first responders content themselves with waiting for you to come out. The hunt is now a siege.";	

Understand "booted/feet" or "booted/foot" as the responders when Bloodthirsty Mob is happening.

Bloodthirsty Mob ends when the location is Base.

When Bloodthirsty Mob ends:
	now iphone-humming? is false;


Part Witch Intro

Witch Intro is a scene. Witch Intro begins when the location is Base.

[When Witch Intro begins:
	say "Ted points to you, then the girl.";]

denise-drain-attempt? is a truth state that varies. denise-heal-attempt? is a truth state that varies.
	
Witch Intro ends sacrificing when denise-heal-attempt? is true and Denise is not comatose.

Witch Intro ends selfishly when denise-drain-attempt? is true and Denise is not comatose.

Instead of going when Witch Intro is happening:
	say "Ted blocks your way and [one of]motions to the girl again[or]nods in the girl's direction[or]gestures toward the girl[in random order]. He's [one of][or][italic type]really [roman type][stopping]insistent on you healing her.";

Instead of exiting when Witch Intro is happening:
	try going north; [any direction will do]
	

Part Explore

[If we follow the static version of this story, we're going to wake up and talk to Denise (see Part Denise First commented out below) ... but after trying to shoehorn that in, I feel I need to play more to interactive fiction's strengths: exploration of a space. Not a ton of exploration here, but it's better than nothing.]

Explore is a scene. Explore begins when Witch Intro ends.

When Explore begins:
	now the player is in Upper Lobby;
	remove the Walkman from play;
	remove the notepad from play;
	now the player is alive; [as in healthy]

Explore ends when the location is End of Corridor.

Part Torture

Torture is a musical scene. Torture begins when Explore is happening for three turns. Torture ends when False Climax begins.

When Torture begins:
	say "Speakers hidden somewhere in the ceiling crackle and spew instrumental music. You claw your face in a mute wail.[paragraph break]The music's volume has been set at precisely the right level -- the organ and violins are soft enough that their notes won't kill you, but they're loud enough to make your nerve endings burn.[paragraph break]'Why are you --' Recognition clouds Hal's face. 'Oh.'";

When Torture begins:
	now playing-music? is true;
	now the player is uncomfortable;

When Torture ends:
	now the player is alive;
	
Part Hal Drama

Hal Drama is a scene. Hal Drama begins when Torture is happening for two turns.

hal-yes? is a truth state that varies.
hal-no? is a truth state that varies.

Instead of going east when the location is Upper Lobby and hal-yes? is false and hal-no? is false:
	say "Hal grabs your arm.";
	follow the scene changing rules;
	
To say walkman-plan:
	say "to somehow put the headphones on her and blast tuba music until she was just as dead as her victims";

When Hal Drama begins:
	say "'I've lost count of how many times the beat witch has drained me,' Hal says. The words that must have been pent up in him during his isolation rise in a rapid babble. 'Each time, I think I'm dead, and each time, I wake up on a different floor with another note on the wall taunting me or inviting me to worship her, and no way to get out. I've never seen her. Even when the sun shines, the night doesn't end. I don't know how long it's been since ... since the others were alive.' He gives his nametag a nibble. 'But this time, I wake up to a woman in a protective suit who looks and behaves like a beat witch. But ... you can't be her, can you? Why would you torture yourself?'[paragraph break]Hal's voice quivers beneath its steel. 'I need to know, witch. Are you here to kill the other one?'[paragraph break]You can indicate YES or NO>";
	if the player consents:
		say "[line break]You nod. Your original plan of begging her to stop is laughable now. Your Plan B was [walkman-plan]. That too was a childish hope. But what else is there?[paragraph break]'Then lead on, witch!'";
		now hal-yes? is true;
	otherwise:
		say "[line break]You shake your head. How can you explain? You're here to stop her, but you never wanted to kill anyone[if Steve is dead] -- even Dr. Steve[end if]. At least, that's what you tell yourself.[paragraph break]It's a lie, and the tape player you had until recently is proof. The truth is ... you were going [walkman-plan] -- but Side A was Plan B, right?[paragraph break]'Then are you here to kill me?' Hal asks. 'No -- don't answer.'[paragraph break]A single teardrop drips down his cheek and soaks into what's left of the floor.[paragraph break]'I don't doubt a beat witch will give me death eventually[if has-attempted-kill-hal? is true] -- even if you couldn't do it with the pipe[end if]. But I suppose I have to take a chance with the devil I just met if I ever want to get out of here.' His smile seems more cringey than before.";
		now hal-no? is true;
	[say "'You - you can't be her,' Hal says. 'I've never seen the beat witch music -- the other one must not like you very much.'[paragraph break]He shakes his head.[paragraph break]'No ... I can't believe it. Witch ... are you here to kill the other one?";]

Report going when Hal Drama is happening:
	say "[one of]Hal tries to help you, but you wave him away. Despite the music, you can walk on your own, and you've spent so many years without being touched that you avoid it when possible. He stays with you anyway[or][hal-going][stopping].";
	now Hal is in the location;	
	
To say hal-going:
	say "[one of]Hal follows you[or]Hal is the shadow you didn't know you had[or]Hal slips behind you noiselessly[or]Where you go, Hal goes -- for now[then at random]";

Every turn when Hal Drama is happening:
	if Hal Drama is happening for more than two turns and the office is not open and the current action is not examining the memo and Margo is not in the location:
		say "[hal-drama-action]";
	[if Hal Drama is happening for three turns:
		say "'So you're a [italic type]good[roman type] monster -- [if hal-yes? is true]and you're only here to kill the other witch?' Hal does not try to hide his expression of disbelief[otherwise]but you're not here to kill the other witch? What [italic type]are[roman type] you here for anyway?' You get the uncomfortable feeling that despite the fact that Hal's business empire is a tomb, he is starting to see you as a bad job interview[end if].";
	otherwise if Hal Drama is happening for four turns:
		say "'I know I came off as a little harsh,' Hal says. 'It's just that I don't like it when my employees don't tell me everything -- or can't, I guess in your case. Do you even have human motivations?' He coughs uncomfortably. 'I suppose ... it must have been ... that is, I can't imagine what it would have been like, being a girl who just hit puberty, waking up one morning to find you can't speak, and you have powers so fearsome that -- did I say something wrong?'[paragraph break]You shake your head for him to forget it. You wish you could.";
	otherwise if Hal Drama is happening for more than four turns:
		if a random chance of 1 in 3 succeeds and the office is closed:
			say "[one of]Hal bites his mangled nametag -- he must be starving[or]Hal mutters something about [hal-muttering][or]Hal looks as though he wants to ask you more questions -- but, of course, all he'll hear from you is silence[or]Hal glances at the ceiling, probably wondering if you'll drain him if the music ever stops[then at random].";]

To say hal-drama-action:
	say "[one of][hal-monster][or][hal-other][stopping]";


To say hal-monster:
	say "'So you're a [italic type]good[roman type] monster -- [if hal-yes? is true]and you're only here to kill the other witch?' Hal does not try to hide his expression of disbelief[otherwise]but you're not here to kill the other witch? What [italic type]are[roman type] you here for?'[paragraph break]You get the uncomfortable feeling that despite the fact that Hal's business empire is a tomb, he is starting to see you as a bad job interview[end if].[paragraph break][hal-flashback]";
	

hal-flashback-spoken is a truth state that varies.

To say hal-flashback:
	if hal-flashback-spoken is false:
		say "'I know I came off as a little harsh,' Hal says. 'It's just irritatingly unprofessional when my employees don't tell me everything -- or can't, I guess in your case. Do you even have human motivations?' He coughs uncomfortably. His face relaxes with a sympathy that could be genuine. 'I didn't mean it like ... that is, I suppose ... it must have been ... I can't imagine what it would have been like, being a girl who just hit puberty, waking up one morning to find you can't speak, and you have powers so fearsome that -- did I say something wrong?'[paragraph break]You shake your head for him to forget it. You wish you could.";
		now hal-flashback-spoken is true;	

To say hal-other:
	if a random chance of 1 in 3 succeeds: 
		say "[one of]Hal bites his mangled nametag -- he must be starving[or]Hal mutters something about [hal-muttering][or]Hal looks as though he wants to ask you more questions -- but, of course, all he'll hear from you is silence[or]Hal glances at the ceiling, probably wondering if you'll drain him if the music ever stops[then at random].";
	otherwise:
		say "[run paragraph on]";

To say hal-muttering:
	say "[one of]not knowing if he's awake[or]being sure he saw a walking corpse from a window recently[or]how hungry he is[at random]";

Hal Drama ends when the location is Breakroom.

Part Little Fight

Little Fight is a scene. Little Fight begins when Explore ends.

Little Fight ends hurtfully when Margo is hurt.

Little Fight ends timefully when Little Fight is happening for three turns.

Little Fight ends when the location is Breakroom.

Before going east when Little Fight is happening:
	now the player is in the Breakroom instead;

attack-with-fists? is a truth state that varies.

Little Fight ends fistly when attack-with-fists? is true;

Little Fight ends willingly when the attack-with-fists? is false and Margo is not hurt and Little Fight is not happening for three turns.

When Little Fight begins:
	say "'Come to the breakroom, Polly Dolly. I have sandwiches. And you can come too, Hal, if you kindly shut up.'";

Definition: A thing is possessed if it is carried by the player.	

Shattering it with is an action applying to two things. 

Understand "attack [something] with [something possessed]"  or "kill [something] with [something possessed]" or "break [something] with [something possessed]" or "fight [something] with [something possessed]" as shattering it with. Understand "stab [something] with [something possessed]" as shattering it with when the player carries the shard.

Instead of shattering something with something:
	try attacking the noun;

Instead of attacking Margo when Little Fight is happening:
	if Polly carries the pipe:
		say "You swing the pipe wide, and it thuds into Margo's skull. She crumples. Hal claps.[paragraph break][if Steve is dead]The scary part about killing is it gets easier every time.[otherwise]The pipe trembles in your hands. You have never killed anyone on purpose. What have you done?[end if][line break]Margo stirs,[if Steve is dead] and without thinking, you bring the pipe down again. Crunch.[otherwise] and you step back in surprise. Her skull is dented, though oddly, she isn't bleeding. No way anyone could shake that off.[end if][line break]'Rude!'[paragraph break]Margo grabs you by the ankles and yanks. You smack into the floor. You lie there, gasping air into your empty lungs as Hal groans.[paragraph break]'Come along, Polly Dolly.'[paragraph break]She drags you through the opening.[paragraph break]Hal whispers despair into his chest as he follows.";
		now Margo is hurt;
	otherwise:
		say "Your fist meets Margo's jaw. You gasp. It's like punching granite.[paragraph break]She snickers and grabs you and Hal by the wrist. Her grip is steel.[paragraph break]'Come along, sillies.'";
		now attack-with-fists? is true;
	now the player is in the Breakroom;

To say margo-drags-you:
	say "Margo's hand grips your wrist so hard, your fingers go numb. She pulls you toward the breakroom[period][paragraph break]'There is no way back, darling. Remember? Sandwiches!'";

To say maybe-quote:
	unless Little Fight is happening:
		say "'";
	otherwise:
		say " ";

Before going west when the location is End of Corridor and Little Fight is happening:
	say "[margo-drags-you]";
	try going east instead;
	
When Little Fight ends timefully:
	say "[margo-drags-you]";

Part Big Surprise

[* Change this up. Have Hal start eating the moldy food, Margo comment how toys can be so ungrateful -- she's been sustaining him with her power, before she drains him over and over.]

Big Surprise is a scene. Big Surprise begins when Little Fight ends. Big Surprise ends when Big Surprise is happening for 3 turns.

Instead of going west when Big Surprise is happening:
	say "[one of]'But the game isn't over, Polly Dolly,' Margo says.[paragraph break][or][stopping]Ted blocks your way.";

[The default describing the player's enclosure rule does nothing. ]

chair-binding? is a truth state that varies.

[The room description body text rule does nothing when the player is on the chairs and Big Surprise is happening. ]

When Big Surprise begins:
	if Polly carries the pipe:
		say "Margo snatches the pipe from your hands.[paragraph break]'What were you going to do with that -- smash my good looks?'[paragraph break]She tosses it behind her, and it crashes beyond the doorway, probably somewhere in the lab.[paragraph break]You are totally defenseless now.[paragraph break]";
		remove the pipe from play;
	if the location is not Breakroom:
		now the player is in Breakroom;	
	now Margo is in Breakroom;
	now Ted is in Breakroom;
	now Hal is in Breakroom;
	say "[last-test-intro]Did you miss him, Hal?'
	
	In the room's darkest corner, Ted stands at one of the tables, staring through you as he did in the first-floor lobby. This time, his hands are over his ears. 
	
	Hal bounds to him and gives him a hug tighter than he gave you. Ted stays rigid.
	
	'It's me! Hal!' No reaction. Hal resorts to blubbering. 'Remember on the stairwell after the explosion? We were running to get help, and you just ... fell down after we passed the girl. Everyone else started dropping dead after that, and I've been so ... don't you --' Hal steps back in disgust and horror. 'Ted ... you're, you're ...'
	
	'I am a goddess of miracles.' Margo takes the remote. She points it at Ted in a grand flourish. 'And your brother's my second-favorite corpse. Anything Dead Ted sees and hears, I see and hear.'";

	
	[say "The newscast shows a familiar porch with a creaky second step. Your dad is shouting at the cameras to leave him alone and that Polly is dead.
	
	The reporter presses further. Has he heard that authorities have identified his daughter as the beat witch responsible for the deaths of millions?
	
	Your dad goes off about how the creature that wears your face isn't you. A demon took the place of his little Polly overnight. That demon killed Polly's mother with a thought.
	
	Of course, the demon wrote over and over that it was an accident, but you dad says he could see through the tears. Still, he couldn't be the one to kill a monster that looked like his daughter. He locked you in the basement with some Enya playing on low and got the police.   
	
	But you escaped, and your older brother was gone -- your second victim. 
	
	Then a montage of photos and pain:
	
	 You and Benny in tuxedos: you with a tuba and him with a harp, both grinning after winning the Young Sibling Music Festival. 
	
	 Your first wanted poster. It bears your sixth-grade photo and the usual instructions for the public to blast music and show you no mercy for safety's sake. 
	
	 Dozens of images of you over the years, including you stealing food from a hot dog stand, disappearing into a national forest, running from people waving boomboxes.
	
	 Surveillance footage of you in an alley near the quarantine area, draining an epidemiologist named Alisha Bridges and taking her hazmat suit and helmet. She ended up surviving -- barely[if Steve is dead]. The same could not be said for the latest victim, one Dr. Steve Patel[end if].    
	
	'We'll keep you updated with the latest from the emergency team inside the city,' the anchor says. 'We'll also take a look back at centuries of beat witch cults. All after the break.'";]
	[press any key;]
	[Now the news shows a faded painting on yellowed parchment. In the center is a woman standing on a platform with her hands above her hand. On one side of her is a crowd of medieval peasants, kings, knights, and ladies raising their arms in supplication. On the other side, those not worshipping the woman are dead.]
	[
	'All after the break.'";]


To say last-test-intro:
	if Little Fight ended hurtfully:
		say "Margo yanks you to your feet. '";
	otherwise if Little Fight ended timefully:
		say "'You don't seem to be in any rush.' Margo says. 'Why don't you relax? ";
	otherwise if Little Fight ended fistly:	
		say "'Your lack of manners is going to get you killed,' Margo says. 'Of course, that will happen anyway. ";
	otherwise:
		say "'";
		
Part False Climax

[False Climax's a mother scene -- that is, it has various scenes that happen under it.]

Chapter False Climax Subscenes

A scene can be climax-child. 

climax-child-reached-limit? is a truth state that varies.

Every turn when a climax-child scene is happening for 5 turns:
	now climax-child-reached-limit? is true;
	
When a climax-child scene begins:
	now climax-child-reached-limit? is false;

To say maggot-text:
	say "A maggot dangles from her lips, and she swallows it"; [We'll be putting this in various climax-child scenes.]

Chapter False Climax Scene (the parent)

False Climax is a scene. False Climax begins when Big Surprise ends.

False Climax ends musically when Musical Walkman ends safely.

False Climax ends margo-dead when False Climax Margo Dangling ends victorious.

False Climax ends margo-alive when False Climax Margo Dangling ends timely.

False Climax ends when the Walkman is in North Side.

When False Climax ends:
	set speakers; [What corpse will speak for Denise?]
	set new hallway descriptions;

[The activity-based room description body text rule is listed before the room description body text rule in the carry out looking rules.

This is the activity-based room description body text rule:
	if Transition is happening:
		follow the scene changing rules;
	otherwise:
		continue the action;]

[This is the watch scenes rule: 
	follow the scene changing rules.]

[The watch scenes rule is listed before the room description body text rule in the carry out looking rules;]

[Before looking when Transition is happening:
	follow the scene changing rules;]
		

To set speakers:
	if Margo is in Breakroom:
		now Margo is speaker;
	otherwise if Hal is in Breakroom:
		now Hal is speaker;
	otherwise:
		now Ted is speaker;


To set new hallway descriptions:
	if Margo is speaker:
		now the description of End of Corridor is "Margo's grin contorts the loose skin on her face.[paragraph break]'[polly-name], there's someone I want you to meet!'[paragraph break]You try to break out of Ted's grip, but his rigor mortis-ed fingers dig into your skin.";
		now the description of Corridor is "'You'll never guess!' Margo says. 'Who knew Christmas could come so early, and -- ooh, if I'm Santa, these must be my elves!'[paragraph break]Ted pauses next to the office door. It opens.[paragraph break]You gag on the stench and stop struggling for a moment.[paragraph break][office-undead].[paragraph break]'Ho, ho, ho, it's off to the North Pole!' Margo says, and you and your undead escorts press forward.[paragraph break][you-fool].";
		now the description of Upper Lobby is "Ted no longer holds you, but it doesn't matter. [corpse-torrent]";
	otherwise if Hal is speaker:
		now the description of End of Corridor is "As soon as you step into the corridor, you know something's wrong.[paragraph break]Hal's eyes grow wide.[paragraph break]'Polly, I don't think I --'[paragraph break]He flops to the ground. He stares past the ceiling into final emptiness.[paragraph break]Your breath hisses and spasms through your teeth -- your version of a scream.[paragraph break]And Hal's dead lips curl into a grin.[keypress][paragraph break]You bolt.";
		now the description of Corridor is "Your legs can't move fast enough.[paragraph break]'But [polly-name], the party isn't over!'[paragraph break][you-fool].[paragraph break]The office door opens. [office-undead].[paragraph break]They block your way. You glance over your shoulder, but there's no escape that way -- Hal is skipping toward you, and Ted is lumbering out of the breakroom. You are an island of life in an ocean of the undead. The current swallows you and presses you forward.";
		now the description of Upper Lobby is "[corpse-torrent]";
	otherwise:
		now the description of End of Corridor is "You defeated the beat witch -- so why is nagging feeling inside you worse than before?[paragraph break]Enough of that. Focus. This isn't the first time you've escaped a building surrounded by people trying to kill you. The parking garage -- yes, if there is one, the mob likely isn't watching that.[paragraph break]You freeze at the footsteps behind you. You don't dare look back.[paragraph break]'That hurt, [polly-name]!' It's a man's voice you haven't heard before, but you know it's Ted's. 'Didn't your mommy tell you it's rude to make people splatter?'[paragraph break]You bolt.";
		now the description of Corridor is "Your legs can't move fast enough.[paragraph break][italic type]Stupid, stupid ...[roman type][you-fool].[paragraph break]The office door opens, and you gag at the stench. [office-undead].[paragraph break]They block your way. You glance over your shoulder, but Ted's there, blowing you a kiss. No escape, no escape.";
		now the description of Upper Lobby is "[corpse-torrent]";	

[When False Climax ends:
	say "False Climax is over!";]

dont-give-room-description? is a truth state that varies.

Instead of going when False Climax is happening and chair-binding? is false:
	say "[if the player is on the chairs]You'll have to fight your way out of this chair first[otherwise]You're a little busy to leave right now. This is why you came[end if]." instead;

When False Climax begins:
	now Hal is on the chairs;
	say "Hal lunges at Margo, but Ted grabs him by the shoulders and plunks him down in a chair. Ted's hands go immediately back to his ears. Is that pain?
	
	Hal is scarlet with grief and anger. 'My brother is no one's puppet -- let him rest in peace, witch!'
	
	'But Hal, dear, I wouldn't have been able to get the building's power generator running without the knowledge rotting inside Teddy's brain. Besides, even if I wanted to, it takes a little time for me to release my hold on a stiff -- time you don't have. And as for [italic type]your[roman type] surprise, dear Polly ... ' She grabs the remote from the table. She points it upward and clicks a button. 

	The music stops. Ted's hands rest at his sides. You can breathe again.
	
	'I suppose I don't need these anymore.' Margo takes out her earplugs and tosses them to Ted, who swallows them.
	
	'Ted, what ...?'
	
	Hal's reaction makes Margo snort. 
	
	She aims the remote at the TV and turns on the news. Then she tosses the remote to Ted. 

	He puts it in his mouth and crunches hard on the plastic. Teeth fly everywhere. Hal cringes. Margo giggles. Apparently, corpses eating random things has a punchline only she can see.
	
	'And guess what, Polly? You're famous!' Margo says. 
	
	[if the player is not on the chairs]She shoves you onto one of the chairs[otherwise]'Stay seated,' she says[end if].";
	now dont-give-room-description? is true;
	now chair-binding? is true;	
	if the player is not on the chairs:
		now the player is on the chairs; 
	follow the eliminate-room-description rule;
	remove the remote from play;
	remove the earplugs from play;
	press any key;
	say "[polly-news]";
	press any key;
	say "The commercial is one for toothpaste. It's an upbeat jingle sung by children. Both you and Margo shake in a wave of agony.
	
	[italic type]
	Pearly Bite toothpaste, O how clean![line break]
	My teeth are the whitest I've ever seen.[line break]
	Sparkle, sparkle, like a diamond mine.[line break]
	Having no cavities is oh so divine![line break]
	La-la-laaaaaahdy-la-la--
	[roman type]
	
	'My remote!' Margo shouts. But Ted has just swallowed the last of it.
	
	She rips the TV from the wall. The device is silent and dark now, but she hurls it out the window in a burst of glass that sends shards everywhere, including some that tear your face.
	
	Margo giggles some more. She pokes her head out of the massive, ragged hole and waits a moment. 'Dag nabbit -- too far to hear it land. I hope it hit one of the yokels down there. Then when someone asks, [']Hey, what's on TV?['], the other person will say, 'You mean, [']What's on Alfred?['] Hahaha.' 
	
	She reaches into her pockets and pulls out your Walkman and notepad. She hands you the notepad but gives the tape player to Hal. Margo manages to keep you seated, pin your arms to your side, and force the earphones -- still attached to the Walkman by a long cord -- onto your head as you struggle. Her grip on your upper arms is nearly tight enough to draw blood.
	
	'Final game for both,' she says. 'That tape is set to side A -- not dear Benny's voice, alas, but a tuba and a harp! Will you kill her with her own music first, Hal? Or will you drain him first, Polly? Think of it, girl -- you could get strong and kill me like you've always wanted! Just be quicker than him.' Giggle. 'Or maybe you'll use your notepad to beg for your life. It's like a cowboy movie! Isn't this delightful? Aaand ... go!'
	
	Your gaze locks with Hal's. You've never been more aware of someone else's life-rhythm -- or of your own.";
	now the window is broken;
	[* Put these things on the southern side of the building.]
	remove the TV from play;
	now the player has the notepad;
	now the Walkman is closed; [in case we opened it before]
	now Hal has the Walkman;
	now the tape-side of the tape is A-side;
	now the player wears the earphones;
	now dont-give-room-description? is false;
	[if Little Fight ended hurtfully:
		say "Margo yanks you to your feet and shoves you into a chair.";
	otherwise if Little Fight ended timefully:
		say "'You don't seem to be in a rush,' Margo says. 'Why don't you relax?'[paragraph break]Without waiting for you to react, she shoves you into a chair.";
	otherwise if Little Fight ended fistly:
		say "Margo shoves you into a chair and whispers, 'Your lack of manners is going to get you killed.'";
	otherwise:
		say "'You are right to be excited about the sandwiches,' Margo says. And with that, she shoves you into a chair.";]

This is the eliminate-room-description rule:
	do nothing instead;

The eliminate-room-description rule substitutes for the room description body text rule when dont-give-room-description? is true.

[The room description body text rule does nothing when dont-give-room-description? is true.]
 [We don't want a needless room description when Polly sits down, especially during our unavoidable (I think unavoidable) text dump!]

To say polly-news:
	say "'-- whom authorities are now identifying as Polly Miller based on descriptions from the emergency response team,' a reporter says.
	
	Images of you slide across the screen in quick succession. 
	
	'... killed half a million people in the city. But it all started with her mother and brother ...'
		
	 A picture of you and Benny in tuxedoes before the Sibling Youth Music Festival. He leans against a harp, and you heft a tuba twice your size.
	
	'... highly dangerous ...'
	
	Your first wanted poster, which bears your sixth-grade student photo and instructions for the public to blast music on sight. 
	
	'... eluding ... running ...  decades ...'
	
	You emerging from hiding in a national forest, you starving and breaking into a grocery store after hours, you running from a crowd with boomboxes. 
	
	'.. monster ...'
	
	The latest surveillance footage: You dragging 'noted epidemiologist Alisha Bridges' into an alley outside the quarantine area after you drained her and borrowed her hazmat suit. No mention of her still being alive, though you made sure she was.

	'More after the break.'[paragraph break]";
	

Chapter False Climax First


False Climax First is a climax-child scene. False Climax First begins when False Climax begins.

False Climax First ends timely when climax-child-reached-limit? is true.

False Climax First ends drainingly when Hal is drained.

False Climax First ends lively when Hal is strong. [We gave him life.]

False Climax First ends givingly when the player is strong.

False Climax First ends notepadingly when showing-notebook? is true.

Before doing something to the earphones when False Climax is happening:
	if the current action is examining or talking to or showing or attacking or shattering:
		continue the action;
	otherwise if the chair-binding? is true:
		say "Margo's grip on your upper arms is icy iron. You can reach the notebook[if has-shard is familiar] and shard[end if] in your lap, but you can't reach the earphones on your head." instead;
		

This is the cant-exit rule:
	if chair-binding? is true:
		say "[exit-forbidden]" instead;
	otherwise:
		continue the action;

To say exit-forbidden:
	say "[one of]'Stay seated, [polly-name],' Margo says. She tightens her grip on your upper arms[or]Margo won't let you out of the chair[stopping]." instead;


[Every turn:
	follow the cant-exit rule;]
	

Before going when chair-binding? is true:
	try exiting;
				
Instead of getting off the chairs when chair-binding? is true:
	try exiting;

Before exiting (this is the stay-seated rule):	
	follow the cant-exit rule;
	
Before attacking when chair-binding? is true:
	follow the cant-attack rule;

This is the cant-attack rule:
	if the noun is the earphones and the earphones are not snapped and the player has the shard:
		say "You grab the shard and slice through the cord in a single motion. The tape is still whirring, but the music has stopped. The shard falls from your numb fingers, and you catch your breath.[paragraph break]'Well, that's a shame,' Margo says. 'Dear Hal, I'm sorry, but you're a useless worshipper. Do give my regards to that good night.'[paragraph break]Before Hal can cry out for mercy, Ted releases him, and Hal slides out of his chair, lifeless.[paragraph break]Margo rips what remains of the earphones from your head, grabs the Walkman from Hal's limp grip, and tosses both things out the window. You grow a chilly kind of numb as you realize what you've lost.";
		now the earphones are snapped;
		remove the earphones from play;
		remove the shard from play;
		now the Walkman is in North Side;
		now Hal is dead;
		now Hal is in Breakroom; [no longer on the chairs]
		follow the scene changing rules instead;
	otherwise if Musical Walkman is happening for more than 0 turns and the noun is Margo and has-shard is familiar:
		say "Margo grips your upper arms so tightly that there's no way you can reach her with the shard." instead;
	otherwise:
		say "[exit-forbidden]" instead;

	
Before attacking (this is the shard-attack rule):
	if chair-binding? is true:
		follow the cant-attack rule instead;			
	[if chair-binding? is true:
		say "[one of]'Stay seated, Polly,' Margo says. She tightens her grip on your upper arms[or]Margo won't let you out of the chair[stopping]." instead;]

[ or going or attacking when chair-binding? is true:
	if the current action is attacking and Musical Walkman is happening for more than 0 turns:
		if the noun is Margo and has-shard is familiar:
			say "Margo grips your upper arms so tightly that there's no way you can reach her with the shard." instead;
	otherwise:
		say "[one of]'Stay seated, Polly,' Margo says. She tightens her grip on your upper arms[or]Margo won't let you out of the chair[stopping]." instead;]

[* Note: When we finally see Denise, she's wild-eyed and half-mad. Impossible but she might have been running longer than you.]	

The stay-seated rule and the shard-attack rule are listed first in the before rules.	


Chapter False Climax Drain

False Climax Drain is a climax-child scene. False Climax Drain begins when False Climax First ends drainingly.
		
False Climax Drain ends when False Climax Margo Earphones begins. [This interrupts our scene.]

Before taking the Walkman when the Walkman is enclosed by Hal and False Climax Drain is happening:
	say "Hal clutches the Walkman tightly against his chest, as though it's a sacred relic. But you don't need the Walkman to deal with Margo." instead;

When False Climax Drain begins:	
	now chair-binding? is false;
	say "Hal's eyes widen when he realizes what you're doing, but he doesn't play the tape.[paragraph break]Ted lets him fall to the ground. Hal is still breathing. He clutches the Walkman.[paragraph break]Your muscles shiver with energy, and your body has a slight glow.[paragraph break]'That's more like it! Let's have some fun!' Margo says. Her grip loosens a little. [maggot-text].[paragraph break]Physical violence isn't normally your thing, but Margo is begging to be the exception.";
	now Hal is in Breakroom; [no longer in his seat]
	[remove the earphones from play;
	remove the Walkman from play;]
	
Before exiting when the player is on the chairs and False Climax Drain is happening:
	say "[one of]With superhuman strength, you break free of Margo's grip.[or][stopping]"

margo-dangling? is a truth state that varies.

Instead of attacking Margo when False Climax Drain is happening:
	if the player is on the chairs:
		try exiting;
	say "You launch yourself at the marketing intern[if the player wears the earphones], and your earphones go flying[end if]. You both crunch into the shattered glass.[paragraph break]Your punches fly[if Margo is hurt], and the dent in her skull deepens[end if].[paragraph break]Margo giggles between your blows.[paragraph break]'I don't think dear Benny would want you doing this,' she says.[paragraph break]You voicelessly snarl, grab her by the collar, and throw her into dangling glass of the window. She catches herself on the ledge.";
	try silently taking off the earphones;
	now margo is dangling;
	follow the scene changing rules;

Instead of attacking Ted when False Climax is happening:
	if Ted is zombie:
		say "You roundhouse kick Ted, and [if Ted carries the Walkman]the Walkman goes flying out of his hands and into the darkness beyond the window.[paragraph break]H[otherwise]h[end if]e crashes into the sandwiches. He slides to the floor, unmoving[one of][or] ... again[stopping].";
		now Ted is dead;
		if Margo is dangling:
			say "[line break]Margo gives you a mocking snort as she hoists herself through the window. The loose glass slicing her skin doesn't bother her.[paragraph break]'Well, that failed miserably, [polly-name]. In fact, your day is about to get worse. Oh, so much worse.'[paragraph break]She wordlessly grabs you by the hair and the seat of your pants. You can't believe how strong she is. She sends you reeling into the corridor.";
			undangle Margo; [which causes the Walkman to be in North Side and Margo to not be dangling]
	otherwise:
		say "You kick Ted while he's down. You get no response.";

[False Climax Drain ends timely when climax-child-reached-limit? is true. ]

False Climax Drain ends margo-dangling when Margo is dangling.


Chapter False Climax Margo Dangling

False Climax Margo Dangling is a climax-child scene. False Climax Margo Dangling begins when Margo is dangling.

False Climax Margo Dangling ends victorious when Margo is dead. 

False Climax Margo Dangling ends failingly when Ted is dead.

False Climax Margo Dangling ends timely when False Climax Margo Dangling is happening for 4 turns.


[False Climax Margo Dangling ends timely when climax-child-reached-limit? is true.]

False Climax Margo Dangling ends helpful when Polly is helpful.

Aiding is an action applying to one thing. Understand "help [someone]" or "aid [someone]" as aiding. Understand "pull [someone] up" or "pull up [someone]" or "help up [someone]" or "help [someone] up" as aiding when Margo is dangling.

Polly can be helpful.

To say margo-dance:
	say "Margo grasps your hands. You try to break free, but she twirls the both of you at a sickening speed.[paragraph break]'It's happy-dance time, [polly-name]!'[paragraph break]Spinning, spinning, spinning.[paragraph break]'I have one last surprise. I guess today's like Christmas, except it ends in death!'[paragraph break]She lets you go, and you are sent tumbling out of the breakroom.";

Instead of aiding someone:
	if the noun is Margo and Margo is dangling:
		say "You will not gamble with your Walkman.[paragraph break]You reach down and pull Margo up.[paragraph break]'You are a wonderful human being.' Margo rises to her feet. 'That was an insult, by the way.'[paragraph break]She backs away from the window. 'Imagine -- this beautiful body could have gone over the edge! Speaking of which ...'[paragraph break]Ted hurls the Walkman over your head, and your beloved tape player vanishes into fog and darkness.[paragraph break]Her smile is too big for her face. 'Oh, no need to lose your temper.'[paragraph break][margo-dance]";
		now Polly is helpful;
		now the Walkman is in North Side;
		now the player is in End of Corridor;
	otherwise:
		say "[if the noun is Denise and Witch Intro is happening]You want to help ... maybe you can give your life to her[otherwise]No one needs or wants your help[end if].";

When False Climax Margo Dangling begins:
	say "Margo's fingers dig into the concrete of the narrow ledge. Her legs swing over nothingness.[paragraph break]'I've had [italic type]gerbils[roman type] of fun, Polly Dolly, but the game is over,' she says.[paragraph break]You get closer. One push ought to do it.[paragraph break]Margo grins as she tries to wiggle up.[paragraph break]'If I go over, Benny does too.'[paragraph break][if the player has the Walkman]Cold hands tear the Walkman from your grasp[period] [end if]You whirl to find Ted with the tape player above his head, ready to toss it into the abyss.[paragraph break]'Be a cranberry sweet tart and help me up, will you, [polly-name]?' Margo says.";
	now Ted is zombie;
	now Ted carries the Walkman;
	try silently taking off the earphones;

Before taking the Walkman when the Walkman is enclosed by Ted:
	say "As fast as you are, Ted is faster. He lobs it over your head, and your Walkman is gone.[paragraph break]You exhale sharply with your mouth so far open it hurts. It's the closest you can come to a primal shriek. Without thinking, you rush at him.[line break]";
	now the Walkman is in North Side;
	try attacking Ted instead;
	
Understand the command "push" as something new.

Understand "push [something]" or "kick [something]" or "destroy [something]" as attacking. Understand "stab [something]" as attacking when the player carries the shard.

To say ted-dies:
	say "Ted crumples into a motionless heap.";

Instead of attacking Margo when False Climax Margo Dangling is happening:
	say "Your foot slams into Margo's[if Margo is hurt] battered[end if] face. She flails and disappears.[paragraph break][if Ted has the Walkman]As she falls, unseen and shrieking, Ted lobs the Walkman over your head, and it also vanishes into the fog.[paragraph break][end if]You suppose Margo meets the ground when you can no longer hear the screams[period][if Ted is zombie][paragraph break][ted-dies][end if]";
	now the Walkman is in North Side;
	now Margo is not dangling;
	now Margo is in North Side;
	now Margo is dead;
	now Ted is dead;

When False Climax Margo Dangling ends timely:
	say "Ted grabs Margo by the arm and yanks her back into the breakroom.[paragraph break]'It's nice to see you're doing nothing useful like a good girl,' she says.[paragraph break]Ted lobs the Walkman over your head. It vanishes into the fog.[paragraph break][margo-dance]";
	undangle Margo;


To undangle Margo:
	now Margo is not dangling;
	now the Walkman is in North Side;
	now the player is in End of Corridor;

	
Chapter False Climax Margo Earphones

False Climax Margo Earphones is a climax-child scene. 

False Climax Margo Earphones begins when False Climax First ends lively. [We drained ourselves and gave Hal most of our lifeforce.]

False Climax Margo Earphones begins when Margo is wearing the earphones and False Climax is happening.

False Climax Margo Earphones ends struggled when Margo is struggling.

False Climax Margo Earphones ends timely when climax-child-reached-limit? is true.

When False Climax Margo Earphones begins:
	say "[if Hal is strong]He breaks through Ted's grip.[paragraph break]'Sorry,' he tells Ted. Still holding the Walkman, Hal lifts his brother's corpse and flings it at Margo. Ted and Margo crash into the sandwiches.[paragraph break]Hal leaps over your head and grabs the earphones from you in a single motion.[paragraph break]Margo is on her feet just as Hal puts the earphones on her head and presses play.[paragraph break][end if]She screams and claws at her head, but [if Hal is strong]Hal presses[otherwise]you press[end if] the earphones harder.[paragraph break][if Hal is strong]Hal drags[otherwise]You drag[end if] her toward the broken window[period][if Hal is strong][paragraph break]'Try draining anyone now, witch[exclamation]'[end if][paragraph break]'Teddy!' Margo whispers.[paragraph break]Ted starts to get up, but he trembles, collapses back into the sandwiches like a masterless marionette and is still.[paragraph break]Margo braces herself against the wall near the window, inches from the edge. Despite the music[if Hal is strong] and Hal's strength[otherwise if the player is buff] and your strength[end if], she's barely managing to stay where she is.[if the player is buff][line break]Her face twists in pain, but she manages to snicker at you. She can tell Hal's strength is already dimming from you. She twists your fingers. Your grip loosens on the earphones.[paragraph break]Margo's eyes go wide, and you sense movement behind you. Hal, in a half-daze, barely managing to stand, wraps his arms around Margo's waist, and pushes his shoulder into her. She clings to him. He does not resist[period][hal-margo-death][end if]";
	now chair-binding? is false;
	now Ted is dead;
	now Hal is in Breakroom; [no longer on the chairs]
	now Margo wears the earphones;
	if the player is drained:		
		now Margo is struggling;
	otherwise:
		now Margo is in North Side;
		now Margo is dead;
		now Hal is in North Side;
		now Hal is dead;
		now the earphones are part of the Walkman;
		now the Walkman is in North Side;

Definition: A person is margo-hal if it is Margo or it is Hal.

To say hal-margo-death:
	say "[paragraph break]The next second seems to last hours. As he and Margo begin their plummet through the teeth of dangling glass and into nothingness, Hal turns and gives you a look that's ... pity?[paragraph break]They both vanish, along with your Walkman. You don't hear anything hit the ground, but Margo's shrieking has faded to silence.";


Chapter Margo's Music

Margo's Music is a semi-musical scene. 

Margo's Music begins when False Climax Margo Earphones begins. 

Margo's Music ends when False Climax Margo Struggling ends.

Chapter False Climax Margo Struggling

False Climax Margo Struggling is a climax-child scene. False Climax Margo Struggling begins when False Climax Margo Earphones ends struggled.  

False Climax Margo Struggling ends victorious when Margo is dead.

False Climax Margo Struggling ends timely when climax-child-reached-limit? is true.

Before aiding Hal when False Climax Margo Struggling is happening:
	try attacking Margo instead;

Before aiding Margo when False Climax Margo Struggling is happening:
	say "[one of]She looks terrified, and despite everything, your first instinct is to help. You move to stop Hal, but Margo glances up at you, and ... are her lips in a mocking curl? And her eyes are so flat, so inhuman. You shake with revulsion[or]You can't make yourself do it[stopping]." instead;

Before attacking a margo-hal person when Margo is struggling:
	if the player is on the chairs:
		try exiting;
	say "You half-crawl toward Margo and Hal. With the last of your strength, you press your body against [noun][']s. They lurch into what remains of the window, taking your Walkman with them.[paragraph break]You steady yourself and don't go with them[period][hal-margo-death]";
	now Margo is in North Side;
	now Margo is dead;
	now Hal is in North Side;
	now Hal is dead;
	now the Walkman is in North Side;
	now Margo is not struggling instead;
	
When False Climax Margo Struggling ends timely:
	say "Hal's glow is dimming. Margo grins. She outlasted his newfound strength. She knees him in the gut, grabs him by the throat, and hurls him out the window. She tosses the Walkman and earphones after him.";
	now Hal is dead;
	now the Walkman is in North Side;


[denise-drain-attempt?]

Chapter False Climax Unaction

[This behaves a little differently from our other False Climax scenes -- due to to the fact that's musical, it comes with its own timer ... OF DEATH!!!]

False Climax Unaction is a scene. 

False Climax Unaction begins when False Climax First ends timely. [We'll add others]

When False Climax Unaction begins:
	if currently-playing? is false:
		say "'I can't take it anymore,' Hal says. 'Sorry, [polly-name].'[paragraph break][start-music]";
		now currently-playing? is true;	
		follow the scene changing rules;

False Climax Unaction ends when Musical Walkman begins.

Part Transition

Transition is a scene. Transition begins when False Climax ends. Transition ends when Prisoner begins.

When Transition begins:
 [No matter how False Climax ends, Ted will always be in the breakroom.]
	[Here's how we start off with the speakers.]
	if Margo is speaker:
		say "The intern picks a maggot from under her fingernail. [paragraph break]'You are such a disappointment, [polly-name].' She squishes the maggot between her thumb and index finger and tosses it over her shoulder. 'But I'm a good hostess. Hal got his little surprise, but haven't forgotten about yours[exclamation]'[paragraph break][if Ted is dead]She snaps, and Ted's blank eyes open. He glides to his feet in a single, breathless movement[period][paragraph break][end if]Without a word from Margo, Ted takes you by the wrist and drags you into the corridor. She follows, clapping and giggling.";
		if Ted is dead:
			now Ted is zombie;
		now Ted is in End of Corridor;
		now Margo is in End of Corridor;
	otherwise:
		say "You slump to the floor for a moment.[paragraph break][italic type]You did it.[roman type][paragraph break]";
		if Hal is speaker:
			if Hal is not dead:
				say "Hal groans, and his eyes flutter into focus.[paragraph break]'She's gone? Really, actually, truly gone?'[paragraph break]You nod.[paragraph break]He motions you to help him up, but you hesitate, and not simply because you shy away from human touch.[paragraph break]'Don't worry about me, Polly,' he says. 'She drained me so often, I'm used it.'[paragraph break]You pull him to his feet. He leans on you.[paragraph break]'A little dizzy ... just give me a moment.'[paragraph break]He focuses on the corridor and avoids glancing at his brother's body.[paragraph break]'Let's go,' he finally whispers."; [* START HERE NOW -- we've got to stop player from giving Hal life here (probably have him refuse it), and bug makes it show it doesn't work to exit the breakroom at this point]
				now Hal is alive;
			otherwise:
				say "Margo is broken on the pavement somewhere. You feel empty and uneasy, though -- maybe it's Hal and Ted lying dead like normal corpses, or the fog drifting ghost-like past the window. Something isn't right.[paragraph break]You tell yourself you're just not used to winning.";
		otherwise:
			say "It's just you and Ted, who lies still, as is the natural way of corpses.[paragraph break]You take a deep breath. With the other witch dead, you can focus on escaping alive. The mob is still waiting for you on the street, no doubt.";
	if the player is not in End of Corridor: [Because there's a chance the player got left behind in the Breakroom!]
		now the player is in End of Corridor;
		follow the scene changing rules;		
		

Chapter Speaker

A person can be speaker. 

he-she-speaker is text that varies. He-she-speaker-caps is text that varies. his-her-speaker is text that varies. His-her-speaker-caps is text that varies. him-her-speaker is text that varies. his-hers-speaker is text that varies.

To set speaker pronouns:
	repeat with P running through persons:
		if P is speaker:
			if P is male:
				now he-she-speaker is "he";
				now He-she-speaker-caps is "He";
				now his-her-speaker is "his";
				now His-her-speaker-caps is "His";
				now him-her-speaker is "him";
				now his-hers-speaker is "his";
			otherwise:
				now he-she-speaker is "she";
				now He-she-speaker-caps is "She";
				now his-her-speaker is "her";
				now His-her-speaker-caps is "her";
				now him-her-speaker is "her";
				now his-hers-speaker is "hers";

Part Prisoner

Prisoner is a scene. Prisoner begins when the location is End of Corridor and False Climax has happened.

When Prisoner begins:
	[now the player is in End of Corridor; -- should already be here. this triggered the scene]
	now the player is in Corridor;
	now the player is in Upper Lobby;


To say you-fool:
	say "You were a fool to think you could stop any of this, that you could save anyone. Benny's faith in you was misplaced";
	
To say office-undead:
	say "Countless bodies shuffle into the corridor. Their faces bubble with burns and rot. Decomposing tissue drips in yellow streams through lab coats. And their eyes -- those with orbs still in their eyesockets give you empty stares";
	
To say corpse-torrent:
	say "The torrent of corpses sweeps you away.[paragraph break]The door to the stairs gives way before unnatural flesh[period][keypress]";


Part Denise First


Denise First is a scene. [Denise First begins when Witch Intro ends.

Denise First ends when Denise First has been happening for two turns.

When Denise First begins:
	say "You awaken to find the pain in your leg is gone.[paragraph break]The girl is sitting on the bottom stair. She has your notepad in her lap and is repeatedly tossing a bloody bullet a few inches in the air like a small ball.[paragraph break]'You [italic type]so[roman type] failed my test,' a child says behind you. '[if denise-heal-attempt? is true]Draining [italic type]yourself[roman type]? Idiot.'[otherwise]For a moment there, I thought you fancied yourself as some sort of savior, but trying to drain a defenseless, adorable little girl shows who you actually are.'[end if][paragraph break]You whirl around. But no one's here but you, Ted and the girl. Why is Ted holding your Walkman? You face the girl again.'";
	remove blood from play;
	now Denise has the bullet;
	now Denise has the notepad;
	now Ted has the Walkman;
	now the player is wearing the earphones; 

[	[paragraph break]'Now I have to ask ... what are you doing in my city?' The girl's mouth still doesn't move, but she gazes at you with such intensity that you are sure the words are hers.[paragraph break]Her grin gives you shivers.[paragraph break]'Let's find out! Oh, Teddy!'[paragraph break]Ted grabs you from behind. He slips on the earphones over your head and somehow manages to hold your flailing arms behind you while pressing play.[paragraph break]'Side B is boring baloney, I'm sure. But [italic type][side-a-name][roman type]? I hope it isn't what I think it is.'[paragraph break]The earphones crackle, and the tape whirs.";
]
	
Every turn when Denise First is happening for one turn:
	say "";
	
Every turn when Denise First is happening for two turns:
	say "[if denise-heal-attempt? is true]Draining [italic type]yourself[roman type]?' the voice continues. [no-mouth]'Idiot.'[otherwise]For a moment there, I thought you fancied yourself as some sort of savior, but draining a defenseless, adorable little girl?' the voice continues. [no-mouth]'I'm not sure you know what you're doing here.'[end if][line break]The girl opens your notepad.[paragraph break]'Let's see if we can't have a conversation, shall we? Ahem. Pleased to meet you, Polly. I'm Denise. Oh, and I was kidding about the pleased part.' [paragraph break] Ted steps between you and her. Is he ... giggling?[paragraph break]She flips through the notepad.[paragraph break]'['][end-suffering][']? Really, people haven't talked like that since 1945.'[paragraph break]You stagger backward.[paragraph break]The voice of the girl  -- Denise -- is coming from Ted's lips.[paragraph break]Denise takes a tiny purple crayon out of her skirt pocket and scribbles on the notepad's pages.[paragraph break]She tosses the notepad back to you.";
	now denise-name is familiar;
	now Denise is not privately-named;
	now the description of the voice is "The girl's voice is Denise's, but it comes from Ted.";
	now the player has the notepad;
	try talking to Denise;

To say no-mouth:
	say "It's a girl's voice, but the girl in front of you is not moving her mouth. "; 

Part Denise Past

Denise Past is a scene. Denise Past begins when Denise First ends.

When Denise Past begins:
	say "A dull aura forms around Denise. It brightens into a solid glow, then a pulsing radiance.[paragraph break]Ted is a thin shadow against the blinding white. He cups his hands to his mouth.[paragraph break]'I AM DENISE, PRINCESS OF LIFE AND DEATH!' he shouts in the girl's voice. (You prefer to think of Ted as talking than Denise talking through him, just as it's easier to think of a sock puppet as speaking, not the puppeteer.)[paragraph break]'I'm guessing you never got a chance to learn sign language either. But I don't need paper to speak anymore, unlike your woefully underpowered self. Corpses are great fun to control.' Ted wraps his arms around himself. 'And Teddy[']s my favorite!'[paragraph break]The light around Denise recedes until she's just a little blonde girl with a dazzling grin. 'What's this, I wonder?'";
	

To say denise-past-intro:
	if Denise is glad:
		say "'Hey, [polly-name].";
		]
				

Part Musical Walkman

[For when the Walkman is playing.]

Musical Walkman is a musical scene. Musical Walkman begins when currently-playing? is true.

Musical Walkman ends deadly when countdown-clock is 4.

Musical Walkman ends safely when the earphones are snapped.

Understand the command "cut" as something new.

Understand "cut [something]" or "slice [something]" or "slice through [something]" or "slice thru [something]" or "cut through [something]" or "cut thru [something]" as attacking when the noun is the earphones.

Understand "cut [something] with [something possessed]" or "slice [something] with [something possessed]" or "cut through [something] with [something possessed]" or "slice through [something] with [something possessed]" or "cut thru [something] with [something possessed]" or "slice thru [something] with [something possessed]" as shattering it with when the player has the shard.

[Before attacking the earphones:
	if Musical Walkman is happening:
		if the player has the shard and the earphones are not snapped:
			say "You grab the shard and slice through the cord in a single motion. The tape is still whirring, but the music has stopped. You set the shard down in your lap as you catch your breath.[paragraph break]'Well, that's a shame,' Margo says. 'Dear Hal, I'm sorry, but you're a useless worshipper.'[paragraph break]Before Hal can cry out for mercy, Ted releases him, and Hal slides out of his chair, lifeless.[paragraph break]Margo rips what remains of the earphones from your head, grabs the Walkman from Hal's limp grip, and tosses both things out the window.";
			now the earphones are snapped;
			remove the earphones from play;
			now the Walkman is in North Side;
			now Hal is dead;
			now Hal is in Breakroom;
			follow the scene changing rules instead;]


When Musical Walkman ends deadly:
	die;

When Musical Walkman begins:
	[if the player carries the earphones:
		now music-harm? is false;]
	now countdown-clock is 0;
	now the player has the shard;

has-shard is a subject.

Every turn during Musical Walkman:
	if countdown-clock is 0:
		say "The familiar harp and tuba tune floods your head with needles.[paragraph break]You notice one of the shards from the broken window ended up in your lap.";
		now has-shard is familiar;
	if countdown-clock is 1:
		say "The lively melody creeps under your skin and gnaws at your insides.[paragraph break]Long ago, you hoped to be half as good on the tuba as Benny was on the harp.";
	otherwise if countdown-clock is 2:
		say "The rhythm suffocates you. You want nothing more than to stop the music, even if it means destroying your property.";
	otherwise if countdown-clock is 3:
		say "The world dims in and out. The only thing that stays in focus is the earphone cord in front of your face.";
	otherwise if countdown-clock is 4:
		say "Fluid drips from your ear.[paragraph break]Maybe somewhere beyond life and breath is a universe where there are no beat witches, and you can bask in numbness forever.";
	[if the tape-side of the tape is A-side:
		if music-harm? is true:
			say "A lively tune crucifies your mind.";
		otherwise:
			say "Lively music whispers harmlessly from the earphones you are not wearing.";
	otherwise:
		if music-harm? is true:
			say "A haunting tune makes your thoughts and body throb with pain.";
		otherwise:
			say "Haunting music whispers harmlessly from the earphones you are not wearing.";]

[Every turn when Musical Walkman is happening and the player wears the earphones:
	now music-harm? is false;]

Musical Walkman ends well when currently-playing? is false. 

When Musical Walkman ends safely:
	now countdown-clock is 0;
	now chair-binding? is false;

Every turn when Musical Walkman is happening:
	if the player wears the earphones:
		now music-harm? is true;
	otherwise:
		now music-harm? is false;
	


[When Musical Walkman ends deadly:
	end the story;
	
Musical Walkman ends deadly when countdown-clock is at least 4.
]


Part Deadly iPhone

iphone-has-played? is a truth state that varies.

Deadly iPhone is a recurring musical scene. Deadly iPhone begins when iphone-deadly? is true. Deadly iPhone begins when iPhone Deadly begins.

When Deadly iPhone begins:
	say "You don't have much time[earphones-no-work]";
	if iphone-has-played? is false:
		now countdown-clock is 0;
		now iphone-has-played? is true; [We don't want to start the countdown clock every time the player takes off the helmet.]

Instead of taking off the helmet when iPhone Deadly is happening:
	say "You refuse to experience the anguish again.";

To say earphones-no-work:
	if the player wears the earphones:
		say ". And the old earphones do nothing to block the music.";
	otherwise:
		say ".";

Before wearing the cowl when a musical scene is happening:
	now iphone-deadly? is false;
	
When Deadly iPhone ends:
	now iphone-deadly? is false;

Deadly iPhone ends well when the player wears the cowl.

Deadly iPhone ends deadly when countdown-clock is 4.

When Deadly iPhone ends deadly:
	die;


Part iPhone Deaf

iPhone Deaf is a recurring semi-musical scene. iPhone Deaf begins when Deadly iPhone ends well.

When iPhone Deaf begins:
	say "The music is muffled now, and you feel better instantly. Your skin tingles with anticipation[period][one of] There's no way you can make it out of here without ... doing what you do.[paragraph break]Dr. Steve's blocking the door. He won't once you DRAIN him.[or][stopping]";

iPhone Deaf ends when the player is not in Staging Area.

Book Margo Wrath

Margo Wrath is a scene. Margo Wrath begins when False Climax has ended and Margo is zombie and Margo is in Breakroom.

[When Margo Wrath begins:]
	[if Polly is helpful:
		say "'You made the right choice, [polly-name]. Falling from a great height is sure to ruin anyone's day.  I want to make it up to you. I insist.'[paragraph break]";]
	[otherwise:
		say "Margo gives you a sneering smile.";[* change]]

Margo Wrath ends when Prisoner begins.





Book Resurrection

Final Scene is a scene.


Volume Taking and Giving Life

zombie-truth is a subject.

thinking-powers-broken is a subject.

To say period-then-period:
	say "[one of][period][or].[stopping]";

To say powers-broken:
	say "[one of]What's happened to you? You've never failed to do this before.[or] Why can't you do this?[or][stopping]";
	now thinking-powers-broken is familiar;

To say powers-broken-hope:
	if thinking-powers-broken is familiar:
		say "You're not sure if this is going to work, but what choice do you have? "; 

Rule for reaching inside a room:
	if the noun is a person:
		say "You're[if the player is wounded] too weak and[end if] too far to affect [Noun]." instead; [* We're going to make an exception to this for when Polly is falling.]
	otherwise:
		say "You're too far away." instead;

To say start-witchy-fail:
	say "You sense the rhythm of Hal's life force and --[line break]";

Book Taking Life

Definition: A thing is personable if it is a person and it is not dead and it is not zombie.

Taking life from is an action applying to one thing.

Understand "take life from [something]" or "take from [something]" or "drain life from [something]" or "drain [something]" as taking life from.


[
Life is a kind of thing.

	
When play begins:
	repeat with P running through people:
		now P has a life.
		
Before taking a life (called L):
	let character be the person enclosing L;
	try silently taking life from the character instead;
]



To say cant-take-life-dead:
	say "Taking life from the lifeless is beyond your ability";

Definition: A thing is unliving if it is not personable or it is zombie or it is dead.


Definition: A person is expired if it is drained or it is dead.

To say pain-leg-gone:
	say "You awaken to find the pain in your leg is gone[period]"; [We use this phrase after Polly tries to give to or drain Denise.]

Instead of taking life from something (called the character):
	if the character is the responders:
		if Steve is expired and the location is Staging Area:
			say "You try to avoid taking more life than you should. You already took [if Steve is dead]more than you needed [end if]from Dr. Steve, and that should be enough to get out of here." instead;
		otherwise if the location is Staging Area:
			say "Since the good doctor is in your way, he's the only responder you need to drain.";
			now the character is Steve;
	[if the character is not personable or the character is zombie or the character is dead:]
	otherwise if the player is drained:
		 say "You are too weak. Your focus is gone."; 
	otherwise if the character is Hal and False Climax is happening:
		if Hal is alive:
			now Hal is drained;
			follow the scene changing rules;
			now the player is strong;
		otherwise if Hal is drained:
			say "You needlessly drain Hal for the last time.[paragraph break][if has-attempted-kill-hal? is true]'You wouldn't kill me with the pipe,' he says into his chest. Then he's gone[otherwise]He sighs and is gone[end if].";
			now Hal is dead;
		otherwise if Hal is dead:
			follow the dead-drain rule;
	otherwise if the character is zombie:
		if zombie-truth is unfamiliar:
			say "[if the player is wounded][one of]You are desperate. [or][stopping][end if][one of]You try to drain life from [the character], but it's like sipping from an empty cup. You sense no life rhythm inside [if the character is male]him[otherwise]her[end if], only a deep silence.[paragraph break][or]For some reason, you can't take life from [the character][period-then-period][stopping][powers-broken]"; [couldn't figure out where an extra period was coming from, so i improvised.]
		otherwise:
			say "[cant-take-life-dead].";
	otherwise if the character is the player:
		say "You cannot take life from yourself -- not unless that life has somewhere to go.";
	otherwise if the character is Denise:
		if Witch Intro is happening:
			say "[powers-broken-hope]If you're going to battle the beat witch, you're going to need the strength more than this girl does.[paragraph break][if Steve is dead]She[']s a casualty of war, like Dr. Steve. Can't be helped.[paragraph break][end if]You close your eyes and find the life-rhythm of her flesh. You try to draw it into yourself. [paragraph break]But the rhythm inside the girl is like a thousand timpanis in rapid staccato. And it's beyond your reach.[paragraph break]You collapse in your own blood ...";
			press any key;
			now denise-drain-attempt? is true;
			now Denise is not comatose;
			follow the scene changing rules; [This triggers Part Explore and ends Witch Intro.]
			if Polly is gray:
				now Polly is evil;
			otherwise if Polly is good:
				now Polly is gray;
		otherwise:
			say "You sense [Denise] has some sort of wall around her life's rhythm.[one of] She smirks.[or][stopping]";
	otherwise:
		if iPhone Deadly has not happened:
			say "You hope you can get out of here without using your awful ability, but that's looking unlikely." instead;
		otherwise:
			if Explore is happening:
				say "[start-witchy-fail]";
				follow the scene changing rules instead;
			otherwise:
				if the character is dead:
					follow the dead-drain rule instead;
				say "[one of]With a thought, you drain life from [character][or]Taking life from [character] is as natural as breathing[or]You take life from [character][then purely at random][period][one of][paragraph break]You love the sensation of new life -- your body inhaling steady rhythm from other flesh, converting it to power. Your senses sharpen, your muscles tighten, and a nearly blinding surge of energy bursts into your veins. You love it so much, you find it disgusting, like gorging on chocolate[period][or][stopping][paragraph break]";
			if the character is alive:
				say "[character] [one of]crashes to the floor[or]collapses[or]faints instantly[then purely at random].[if character is Steve][line break][one of]You were careful not to take too much, though. He'll be better in a few hours, as long as you don't drain him again when he's like this[or]'Not again,' he mutters before drifting into a heavy stupor[stopping][end if].";
				now the character is drained;
			otherwise if the character is drained:
				say "[character] takes a final, sighing breath. [if the location is Staging Area]Someone screams at you, the monster.[paragraph break][end if][one of]All these years of stopping short of killing, and you finally succumbed to your appetite.[paragraph break]You want to throw up.[or][stopping]";
				now the character is dead;
				now Polly is gray; [neutral]
		if the player is alive:
			now the player is strong;
		otherwise if the player is strong:
			now the player is stronger;
			[if the location is Staging Area:
				continue the action;]
			[otherwise:
				now the player is godly;]

This is the dead-drain rule:
	if the noun is dead or the noun is nonliving: [nonliving = regular non-person thing, like a table]
		if the noun is part of the player:
			try taking life from the player;
		otherwise:
			say "[cant-take-life-dead].";

Understand "throw up" or "puke" or "vomit" or "throwup" as a mistake ("A cold guilt spreads from your stomach to joints. Just thinking about Dr. Steve makes it hard to breathe.") when Steve is dead.

Taking life from someone is witchy behavior.
  

Book Giving Life

Giving life to is an action applying to one thing.

Understand "give life to [something]" or "give to [something]" or "heal [something]" as giving life to.

Giving life to someone is witchy behavior.

To say cant-give-life-dead:
	say "Giving life to the lifeless is beyond your ability";
	
Instead of giving life to something (called the character):
	if Explore is happening:
		say "[start-witchy-fail]";
		follow the scene changing rules instead;
	otherwise if the character is zombie:
		if zombie-truth is unfamiliar:
			say "[if the player is wounded]You feel strangely compelled to give what little life you have to [the character].[paragraph break][end if][one of]It doesn't work. You feel your life's rhythm fading out you, only to come roaring back into your body[or]You try to give life to [the character], but it's as if you're not a beat witch[stopping].[powers-broken]";
		otherwise:
			say "[cant-give-life-dead].";
	otherwise if the character is dead or the character is nonliving:
		if the character is part of the player:
			try giving life to the player;
		otherwise:
			say "[cant-give-life-dead].";
	otherwise if the character is the player:
		say "You can't give life from yourself, to yourself[if the player is wounded], as much as your leg pains you[end if].";
	otherwise:
		if the character is Steve:
			if Steve is alive:
				say "Giving life to a healthy Dr. Steve wouldn't endear him to you.";
			otherwise:
				say "[one of]He looks so helpless, lying there ...[or]Your sense of mercy gets the better of you.[stopping][line break][heal-steve]";
		otherwise if the player is drained:
			say "You can't give any more life than you already have.";
		otherwise if the character is Hal and Hal is alive:
			say "You let as much life as you can spare flow out of you and into Hal. He raises a hand to his face. He's glowing a little.";
			now Hal is strong;
			now the player is drained;
		otherwise if the character is Denise:
			if Witch Intro is happening:
				say "[powers-broken-hope]This is it. You will die. But how can you leave her like this?[paragraph break]You will your life into her body. Her chest rises sharply. The world fades into soundlessness ...";
				press any key;
				now Denise is not comatose; 
				now denise-heal-attempt? is true;
				now Polly is good; [She already is, unless she's killed Dr. Steve.]
				follow the scene changing rules; [This triggers Denise First and ends Witch Intro.]
			otherwise:
				say "Giving life to Denise is the opposite of what you're trying to do.";
[				say "[one of]'I appreciate you trying to get into my good graces, but your life energy is a penny compared to my Fort Knox,' she says.[if Witch Intro ended sacrificing] 'I do appreciate what you tried to do for me earlier, but it only proves how dumb you are. Keep the feeble life inside you.'[end if][or]Giving life to Denise is .[stopping]";
][				say "[one of]Denise giggles. 'That tickles. You know you can't drain another beat witch.'[or]Draining another beat witch is impossible.[stopping]" ;]
		otherwise:
			say "Now's not the time for healing."; [We should never get here.]
			

heal-steve-num is a number that varies. heal-steve-num is 0.

To say heal-steve:
	if heal-steve-num is 0:
		say "And you hesitate. You let the newfound strength flow out of your body back into Dr. Steve.[paragraph break]He blinks in surprise and staggers to his feet.";
		increase heal-steve-num by 1;
		now Steve is alive;
		now the player is alive;
	otherwise:
		say "You heal Dr. Steve again. This time, he's not so confused.[paragraph break]'What are you waiting for? [italic type]Shoot her[roman type]!'[paragraph break]A shot goes off. You stumble backward. Blood bursts from your chest in a spurting rain.[paragraph break]As your vision clouds, you try to find even a little sympathy on someone's face. You find none.[paragraph break]A beat witch is death. A beat witch is suffering.";
		die;	
	

Book Can't Be Witchy With Music

[No giving life or taking it when music is loud enough!]

Before witchy behavior when a musical scene is happening (this is the musical rule):
	unless iphone-humming? is true:
		say "You can't focus, not without blocking out or stopping the sound. Music is agony." instead;


Volume Music Sound

[Every turn when Entire Game is happening for two turns:
	play music file "straitjacket-tuba.mp3", looping;

Every turn when Entire Game is happening for three turns:
	execute JavaScript command "let music = document.querySelector('.vorple-music'); if (music) music.volume = 0.2";]



[let myMusic = document.querySelector('.vorple-music');
myMusic.volume = 0.2; 
]
		

Volume Messages

[The standard respond to final question rule response (A) is "You are obstinate even in death. Give one of the answers above."["Please give one of the answers above."]]

The parser error internal rule response (X) is "What?" ["I beg your pardon?"]

The block vaguely going rule response (A) is "In what compass direction?" ["You'll have to say which compass direction to go in."]

The immediately undo rule response (B) is "There is nothing to undo." ["You can't 'undo' what hasn't been done!"]

The immediately undo rule response (E) is "Undone.". ["[bracket]Previous turn undone.[close bracket]"]

The parser command internal rule response (A) is "Uncorrectable." [ "Sorry, that can't be corrected."]

The parser command internal rule response (B) is "Stop wasting time with politeness." ["Think nothing of it."]

The parser command internal rule response (C) is "'Oops' can only correct a single word. Oops." ["'Oops' can only correct a single word."]

The parser error internal rule response (S) is "To repeat a command, you only need to type the word AGAIN or G." ["To repeat a command like 'frog, jump', just say 'again', not 'frog, again'."]

The parser command internal rule response (D) is "You can't repeat that." ["You can hardly repeat that."]

The parser error internal rule response (T) is "You can't begin with a comma; what would the semicolons think?" ["You can't begin with a comma."]

[The parser error internal rule response (U) is "To talk to someone, just type the person's name. But that person normally needs to be nearby." ["You seem to want to talk to someone, but I can't see whom."] ]

The parser error internal rule response (W) is "Simply TALK TO someone if you have the notepad." ["To talk to someone, try 'someone, hello' or some such."]

To say unknown-command:
  say "Please rephrase that[period]";

The action processing internal rule response (K) is "[unknown-command]". ["I didn't understand that instruction."]

The parser error internal rule response (A) is "[unknown-command]". ["I didn't understand that sentence."]

The block attacking rule response (A) is "[if Musical Walkman is happening]You should worry about breaking something else[otherwise]Physical violence really isn't your thing[end if]." ["Violence [aren't] the answer to this one."]

The block burning rule response (A) is "Enough things burn." ["This dangerous act [would achieve] little."]

The block buying rule response (A) is "Nothing to buy." ["Nothing [are] on sale."]

The report jumping rule response (A) is "[if the player is wounded]After your leg's been shot?[otherwise]You are comfortable at your current height.[end if]" ["[We] [jump] on the spot."]


Instead of kissing someone:
	say "Ew.";

The report touching other people rule response (A) is "You don't like to touch or be touched." ["[The noun] [might not like] that."]

The parser error internal rule response (D) is "Math won't get you out of this one." ["I didn't understand that number."]

The parser error internal rule response (E) is "You see nothing like [regarding the noun][those]." ["[We] [can't] see any such thing."]

The can't wear what's not held rule response (A) is "[We] [don't] have [regarding the noun][those]." ["[We] [aren't] holding [regarding the noun][those]!"]

The parser error internal rule response (H) is "That verb only uses one object." ["You can't use multiple objects with that verb."]

The parser error internal rule response (L) is "You excepted something not included anyway, but it's all good." ["You excepted something not included anyway!"]

The parser error internal rule response (K) is "[We] [can't] see ['][pronoun i6 dictionary word]['] ([the noun]) right now." ["[We] [can't] see ['][pronoun i6 dictionary word]['] ([the noun]) at the moment."]

parser error internal rule response (O) is "Don't worry about that." ["That's not something you need to refer to in the course of this game."]


Definition: A thing is other-held if it is carried by an other person.

Rule for deciding whether all includes other-held things: it does not.

The parser nothing error internal rule response (B) is "Nothing to take." ["[There] [adapt the verb are from the third person plural] none at all available!"]


The action processing internal rule response (D) is "You need a noun here." ["You must supply a noun."]

The action processing internal rule response (E) is "You don't need a noun here." ["You may not supply a noun."]


The standard looking under rule response (A) is "Nothing interesting here." ["[We] [find] nothing of interest."]

The can't search unless container or supporter rule response (A) is "Nothing interesting here." ["[We] [find] nothing of interest."]

The can't search closed opaque containers rule response (A) is "Closed. Open [the noun] first." ["[We] [can't see] inside, since [the noun] [are] closed."]

To say rhetorical-means:
  say "Do you know what 'rhetorical' means";

[To say maybeYesNo:
  say "Uh, [one of]maybe[or]indeed[or]nope[or]yeah[or]could be[purely at random]";]

The block saying yes rule response (A) is "[rhetorical-means]?" ["That was a rhetorical question."]

The block saying no rule response (A) is "[rhetorical-means]?" ["That was a rhetorical question."  ]

The block sleeping rule response (A) is "If sleep is heaven, you are the fallen angel of insomnia." ["[We] [aren't] feeling especially drowsy."]

The report smelling rule response (A) is "Everything reeks of smoke and rot." ["[We] [smell] nothing unexpected."]

The can't take yourself rule response (A) is "You're not sure you want yourself, to be honest." ["[We] [are] always self-possessed."]

The can't take other people rule response (A) is "You don't want [noun]. Or anyone." ["I don't suppose [the noun] [would care] for that."]

The can't take people's possessions rule response (A) is "[if the noun is the pistol]You'd be shot before you even touched the gun[otherwise]You're not much of a thief[end if]." ["[regarding the noun][Those] [seem] to belong to [the owner]."]

The can't take items out of play rule response (A) is "[regarding the noun][Those] [aren't] for you to take." ["[regarding the noun][Those] [aren't] available."]


To say lugging:
	say "[regarding the noun][They're] not exactly something you want to lug around";

The can't take scenery rule response (A) is "[lugging]." ["[regarding the noun][They're] hardly portable."]

The can't take what's fixed in place rule response (A) is "[lugging]." ["[regarding the noun][They're] fixed in place."]


The can't pull scenery rule response (A) is "[lugging]"; ["[We] [are] unable to."]

The block thinking rule response (A) is "Deliberate thinking is the recipe for memory's poison." ["What a good idea."]

The futile to throw things at inanimate objects rule response (A) is "That wouldn't do anything." ["Futile."]

block throwing at rule response (A) is "Your aim is too awful to try." ["[We] [lack] the nerve when it [if story tense is the past tense]came[otherwise]comes[end if] to the crucial moment."]

The report touching things rule response (A) is "Everything in this city seems to be covered in an invisible layer of slime." ["[We] [feel] nothing unexpected."]

slime is a backdrop. slime is everywhere. understand "layer" or "layer of slime" as the slime.

Instead of doing something to the slime:
	try touching the noun;
	

The standard report waiting rule response (A) is "Moments pass." ["Time [pass]."]

The block waking up rule response (A) is "Reality's only waking is death." ["The dreadful truth [are], this [are not] a dream."]

Understand "dance" or "twirl" or "dance with [someone]" as a mistake ("You hate dancing.");

Understand "sing" as a mistake ("Your songs are silent.");

Instead of rubbing something:
	say "Cleaning anything in this city is futile.";

Understand "clean up [something]" as rubbing.